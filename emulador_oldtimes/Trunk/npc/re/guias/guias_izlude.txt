//===== Cronus Script ========================================
//= Guias da Cidade de Izlude
//===== Por: =================================================
//= mkbu95
//===== Versão: ==============================================
//= 1.1
//===== Compatível com: ======================================
//= Cronus SVN
//===== Comentários adicionais: ==============================
//= 1.0 Versão inicial [mkbu95]
//= 1.1 Revisado e otimizado [mkbu95]
//============================================================

izlude,121,87,6	script	Guia#izl1	105,{
	mes "[Guia de Izlude]";
	mes "Bem-vind"+(Sex?"o":"a")+" a Izlude, a cidade-satelite de Prontera.";
	mes "Nós estamos aqui para ajudar os visitantes da cidade. Se você tem alguma dúvida, consulte-nos.";
	while (1)
	{
		next;
		switch (select ("Indicações:Limpar indicações:Tutorial:Cancelar."))
		{
			case 1:
				mes "[Guia de Izlude]";
				mes "Por favor, selecione uma das opções.";
				if (!.@compass_check)
				{
					mes "Você gostaria que as indicações permaneçam no mini-mapa?";
					if (select ("Sim.:Não.") == 1) set .@compass_check, 1;
				}
				while (1)
				{
					next;
					switch (select ("^FF0000Associação dos Espadachins^000000:Sede dos Espadachins:Arena:Porto de Izlude:Loja de Armas:Loja de Ferramentas:Cancelar"))
					{
					
						case 1:
							mes "[Guia de Izlude]";
							mes "A Associação se encontra na ilhinha do extremo oeste.";
							if (.@compass_check) viewpoint 1,52,140,0,0xFF0000;						
							break;
					
						case 2:
							mes "[Guia de Izlude]";
							mes "A Sede se encontra na ilhinha do extremo leste.";
							if (.@compass_check) viewpoint 1,214,130,1,0x00FF00;					
							break;
						
						case 3:
							mes "[Guia de Izlude]";
							mes "A Arena se encontra no extremo norte de Izlude.";
							mes "Porém ela ainda não está funcionando.";
							if (.@compass_check) viewpoint 1,128,225,2,0x00FF00;						
							break;
													
						case 4:
							mes "[Guia de Izlude]";						
							mes "Do Porto é possível ir até Alberta ou a ilha de Byalan, nosso calabouço. Ela se localiza ao nordeste.";
							if (.@compass_check) viewpoint 1,200,180,3,0xFF0000;					
							break;
						
						case 5:
							mes "[Guia de Izlude]";
							mes "A Loja de Armas se encontra ao noroeste do centro da cidade.";
							if (.@compass_check) viewpoint 1,111,149,4,0xFF00FF;						
							break;
						
						case 6:
							mes "[Guia de Izlude]";
							mes "A Loja de Ferramentas se encontra ao nordeste do centro da cidade.";
							if (.@compass_check) viewpoint 1,148,148,5,0xFF00FF;					
							break;
						
						case 7:
							set .@compass_check, 1;
							mes "[Guia de Izlude]";
							mes "Se você deseja apagar as indicações do seu mini-mapa, por favor selecione 'Limpar indicações'.";
							close;
					}
				}	
				break;
			
			case 2:
				viewpoint 2,52,140,0,0xFF0000;
				viewpoint 2,214,130,1,0x00FF00;
				viewpoint 2,128,225,2,0x00FF00;
				viewpoint 2,200,180,3,0xFF0000;
				viewpoint 2,111,149,4,0xFF00FF;
				viewpoint 2,148,148,5,0xFF00FF;
                   		set .@compass_check, 0;
				break;
				
			case 3:
				mes "[Guia de Izlude]";
				mes "Nós fomos exaustivamente treinados para servi-l"+(Sex?"o":"a")+" da melhor maneira possivel e sem falhas.";
				mes "Assim você tem a garantia das melhores indicações possiveis.";
				next;					
				mes "[Guia de Izlude]";
				mes "As indicações que lhe daremos podem ser marcadas no seu mini-mapa.";
				mes "Para visualizar o seu mini-mapa, aperte 'ctrl+tab', ou clique no botão 'mapa' no menu principal.";
				mes "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
				break;
				
			case 4:
				mes "[Guia de Izlude]";
				mes "Espero que você tenha uma jornada segura e aproveite sua estadia.";
				close;
		}
	}
}
