//===== Cronus Script =====================================
//= Estalagem
//===== Por: ==============================================
//= SoulBlaker
//===== Versão Atual: =====================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Versão Atual: ===================================== 
//= 2.8a
//===== Descrição: ========================================
//= Estalagem, Salvar e Curar.
//============================================================
//= variáveis:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Cometários adicionais: ============================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=	  and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.7a Traduzido e Otimizado. [RoM]
//= 2.8 Removido Moscóvia; faz parte das quests. [RoM]
//= 2.8a Removido show [Yum]
//=========================================================

//======================== Prontera ===================================
// Estalagem do Oeste -------------------
prt_in,244,135,2	script	Estalajadeira#Sammy	53,{

	callfunc "F_InnMaid","[Funcionária Sammy]","Estalagem do Oeste","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// Hospedaria do Leste -------------------
prt_in,61,141,2	script	Estalajadeira#Ahlma	53,{

	callfunc "F_InnMaid","[Funcionária Ahlma]","Estalagem do Leste","prt_in",64,136;
	warp "prt_in",60,166; end;
}


//======================== Alberta ====================================
alberta_in,32,142,3	script	Estalajadeira#Jennie	53,{

	callfunc "F_InnMaid","[Funcionária Jennie]","'Estalagem do Pescador'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Estalajadeira#Cena	53,{

	callfunc "F_InnMaid","[Funcionária Cena]","'Ifrit,' a única estalagem na cidade de Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}


//======================= Payon ======================================
payon_in01,132,62,5	script	Estalajadeira#Ahee	53,{

	callfunc "F_InnMaid","[Funcionária Ahee]","Estalagem de Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// Hospedaria do Nordeste ----------------------
/*
morocc_in,147,141,3	script	Estalajadeira#Hasna	53,{

	callfunc "F_InnMaid","[Funcionária Hasna]","Estalagem de Morroc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// Hospedaria do Sul ---------------------------
morocc_in,80,100,5	script	Estalajadeira#Manar	53,{

	callfunc "F_InnMaid","[Funcionária Manar]","Estalagem de Morroc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Estalajadeira#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Estalagem de Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Estalajadeira#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Estalagem de Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Funcionário do Hotel#01	86,{

	mes "[Funcionário do Hotel]";
	mes "Bem-vindo ao";
	mes "Royal Dragon,";
	mes "onde você vai encontrar";
	mes "as melhores acomodações";
	mes "e os melhores serviços.";
	next;
	switch(select("Salvar","Descansar - 5,000 zeny","Cancelar")) {
	case 1:
		savepoint "lhz_in02",209,275;
		mes "[Funcionário do Hotel]";
		mes "Muito obrigado, seu";
		mes "Ponto de Retorno foi";
		mes "salvo aqui, no";
		mes "Royal Dragon.";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[Funcionário do Hotel]";
			mes "Desculpe-me, mas você parece";
			mes "não ter zeny o suficiente. Por favor";
			mes "volte quando tiver!";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[Funcionário do Hotel]";
		mes "Muito obrigado~";
		mes "Espero que você aproveite";
		mes "sua estadia aqui";
		mes "no Royal Dragon.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		sc_start SC_BLESSING,240000,10;
		skilleffect AL_BLESSING,0;
		end;
	case 3:
		mes "[Funcionário do Hotel]";
		mes "Muito obrigado e";
		mes "tenha um ótimo dia.";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Estalajadeira#hug	53,{

	callfunc "F_InnMaid","[Recepcionista]","Estalagem de Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Chefe da Estalagem#vei	709,{
	mes "[Chefe da Estalagem]";
	mes "Bom dia~";
	mes "Bem-vindo à mais";
	mes "confortável Estalagem";
	mes "aqui em Veins~";
	next;
	switch(select("Salvar","Descansar -> 5000 zeny","Cancelar")) {
	case 1:
		mes "[Chefe da Estalagem]";
		mes "Seu Ponto de Retorno";
		mes "foi salvo em Veins.";
		mes "Curta sua estadia na cidade~";
		savepoint "ve_in",157,209;
		close;
	case 2:
		mes "[Chefe da Estalagem]";
		if(Zeny < 5000){
			mes "Desculpe-me, mas você não tem a quantidade necessária de zeny para fazer o check in.";
			mes "O serviço custa 5000 zeny.";
			close;
		}
		mes "Curta sua estadia~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		close;
	}
}

//----------------------- Funções da Hospedagem ---------------------
function	script	F_InnMaid	{
	mes getarg(0);
	mes "Bem-vindo à";
	mes getarg(1) + ".";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Salvar:Descansar - 5.000z:Cancelar")){
	case 1:
		mes getarg(0);
		mes "Seu Ponto de Retorno foi salvo.";
		mes "Obrigado e volte sempre.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		mes getarg(0);
		if(Zeny < 5000){
			mes "A taxa de serviço é 5.000 zeny.";
			mes "Por favor, tenha certeza de que tem a quantidade necessária";
			mes "de zeny para o serviço.";
			close;
		}
		mes "Obrigado.";
		mes "Eu espero que você";
		mes "aproveite sua estadia~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
