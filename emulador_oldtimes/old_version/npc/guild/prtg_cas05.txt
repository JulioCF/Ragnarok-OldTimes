//===== Cronus Script =========================================
//= Guerra do Emperium (Prontera)
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: =========================================
//= 1.3/1.2b
//===== Compatível com: =======================================
//= Cronus SVN
//===== Descrição: ============================================
//= NPCs relativos ao 5º Castelo de Prontera
//===== Comentários Adicionais: ===============================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Corrected treasure room switch coordinates. [L0ne_W0lf]
//= 1.3 Traduzido ao Português-BR de acordo com o bRo [Asus]
//= 1.2 Corrigido erros [Soulblaker]
//= 1.2a Revisado. [RoM]
//= 1.2b Removido show	[Yum]
//============================================================

// Guild Flag (Cidade)
//============================================================
/*prontera,162,197,5	script	Gondul#p5-1::Gondul	722,{

	callfunc "Guild_Flags", "prtg_cas05";

OnRecvCastlePt05:
	FlagEmblem GetCastleData("prtg_cas05",1);
	end;
}*/

// Guild Flag (Fora do Castelo)
//============================================================
prt_gld,199,243,2	script	Gondul#p5-2::Gondul2	722,{
	set .@GID, GetCastleData("prtg_cas05",1);
	if (.@GID == 0) {
		mes "[ Decreto do Reino de Rune-Midgard ]";
		mes " ";
		mes "1. Conforme a ordem do Reino de Rune-Midgard,";
		mes "declaro que não há nenhum";
		mes "dono formal deste castelo.";
		mes " ";
		mes "2. Se o seu Clã conseguir";
		mes "passar por todos os desafios";
		mes "e destruír o Emperium deste castelo,";
		mes "você será nomeado como o dono deste castelo.";
	}
	else {
		if (getcharid(2) == .@GID) {
			mes "Bravo guerreiro...";
			mes "Você quer retornar ao seu honorável lugar?";
			next;
			if (select("Voltar ao castelo do seu Clã:Cancelar") == 1) {
				if (getcharid(2) == GetCastleData("prtg_cas05",1)) {
					warp "prtg_cas05",52,41;
					end;
				}
			}
			close;
		}
		mes "[ Decreto do Reino de Rune-Midgard ]";
		mes " ";
		mes "1. Conforme o Pacto do Reino de Rune-Midgard,";
		mes "declaro que este castelo pertence";
		mes "ao Clã ^ff0000" + GetGuildName(.@GID) + "^000000.";
		mes " ";
		mes "2. O Líder do Clã ^ff0000"+ GetGuildName(.@GID) + "^000000 se chama";
		mes "^FF0000" + GetGuildMaster(.@GID) + "^000000.";
		mes "Se alguém se opõe a este decreto";
		mes "desembainhe sua espada para fazer valer sua vontade.";
	}
	close;

OnRecvCastlePt05:
	FlagEmblem GetCastleData("prtg_cas05",1);
	end;
}

prt_gld,199,236,2	duplicate(Gondul2)	Gondul#p5-3	722
prt_gld,197,243,2	duplicate(Gondul2)	Gondul#p5-4	722
prt_gld,197,236,2	duplicate(Gondul2)	Gondul#p5-5	722

// Guild Flag (Dentro do Castelo)
//============================================================
prtg_cas05,19,247,4	script	Gondul#p5-6::Gondul3	722,{
	end;

OnRecvCastlePt05:
	FlagEmblem GetCastleData("prtg_cas05",1);
	end;
}

prtg_cas05,19,243,4	duplicate(Gondul3)	Gondul#p5-7	722
prtg_cas05,26,247,4	duplicate(Gondul3)	Gondul#p5-8	722
prtg_cas05,26,243,4	duplicate(Gondul3)	Gondul#p5-9	722
prtg_cas05,249,289,4	duplicate(Gondul3)	Gondul#p5-10	722
prtg_cas05,256,289,4	duplicate(Gondul3)	Gondul#p5-11	722
prtg_cas05,253,271,4	duplicate(Gondul3)	Gondul#p5-12	722
prtg_cas05,273,257,4	duplicate(Gondul3)	Gondul#p5-13	722

// AGIT Manager
//============================================================
prtg_cas05,266,266,0	duplicate(Gld_Agit_Manager)	Agit#prtg_cas05	-1

// Steward
//============================================================
prtg_cas05,36,37,4	duplicate(Gld_Mngr_Template)	Raynor#prtg_cas05	55

// Guardians
//============================================================
prtg_cas05,36,37,4	duplicate(Gld_Guard_Template)	Guardian#prtg_cas05	-1

// Serviço Kafra
//============================================================
prtg_cas05,52,41,4	duplicate(Gld_Kafra_Template)	Kafra#prtg_cas05	117

// Saída da Sala do Mestre
//============================================================
prtg_cas05,281,176,0	script	#switch_Pt05	111,{
	mes " ";
	mes "Há uma pequena alavanca, você deseja acioná-la?";
	next;
	if(select("Acionar.:Não acionar.") == 2) close;
	warp "prtg_cas05",36,38;
	end;
}

// Guild Treasure Protection
//============================================================
prtg_cas05,276,178,0	duplicate(Gld_Trea_Protect)	ban_warp#prtg_cas05	-1,6,6

// Guild Treasure Spawns
//============================================================
prtg_cas05,276,178,0	duplicate(Gld_Trea_Spawn)	Treasure#prtg_cas05	-1

// Guild Dungeon Switch
//============================================================
prtg_cas05,212,94,0	duplicate(Gld_Dun_Template)	#prtg_cas05	111
