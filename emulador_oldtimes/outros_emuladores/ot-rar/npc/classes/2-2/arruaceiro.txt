//===== Cronus Script ======================================== 
//= Script Arruaceiro
//===== Por: =================================================
//= kobra_k88
//===== Versão Atual: ======================================== 
//= 2.2/2.3a
//===== Descrição: =========================================== 
//= Muda a classe para Arruaceiro
//===== Changelog: =========================================== 
//= Traduzido [Trevor]
//= Organizado [Kamper]
//= Quest Atualizada [Mehah]
//============================================================


//=======================================================================================================//
//					Markie: Test 1 and Jobchange						//
//***********************************************************************************************************************************************************//
in_rogue,363,123,3	script	Markie	747,{

	show "[Markie]";
	if(BaseJob == Job_Thief) goto L_Thief;
	if(BaseJob == Job_Rogue) goto L_Rogue;

L_JobOther:
	show "Eu não sei como você achou essse lugar, mas eu acho que é melhor você sair daqui..........";
	close;

L_Rogue:
	show "Bom, ver você de novo, amigo. Você deve estar se divertindo como Arruaceiro hum? Heh he he....";
	close;

L_Thief:
	if(JobLevel < 40){
		show "Oh! Olá! Se você quer se tornar um Arruaceiro eu recomendo que você volte quando tiver treinado mais um pouco.";
		show "Somente Ladrões com um ^5533FFNível de Classe no mínimo 40^000000 pode se tornar um Arruaceiro.";
		close;
	}
	if(ROGUE_Q == 1) goto L_ReTest;
	if(ROGUE_Q == 2) goto L_Test2;
	if(ROGUE_Q == 3) goto L_Test3;
	if(ROGUE_Q == 4) goto L_Test4;
	if(ROGUE_Q == 5) goto L_Change;
	show "Humm? O que traz você aqui? Oh! Entendi agora... Você quer se tornar Arruaceiro, não é isso?";
	next;
	show "[Markie]";
	show "Bom, legal te encontrar aqui. Meu nome é Markie, Quem é você?";
	next;
	show "[Markie]";
	show strcharinfo(0) + "..... He he, você tem um bonito nome amigo. Bem, como você decidiu se tornar um Arruaceiro?";
	next;
	show "[Markie]";
	show "Bem, como você honesta comigo e me deu o seu nome de verdade eu acho que isso não importa.";
	show "Mas, só para que você saiba, um Arruaceiro nunca revela seu verdadeiro nome para ninguém.";
	next;
	show "[Markie]";
	show "É a regra número 1, então, a partir de agora é melhor que você se cuide com isso.";
	show "Bom, aqui tem um formulário pra você..... por favor, coloque todas as informações necessárias.....";
	next;
	show "(você escreve todas as informações necessárias para completar o formulário e o devolve)";
	set JBLVL, JobLevel;			// used to determine what item to get at the end
	next;
	show "[Markie]";
	show "Ok docinho.... parece bom, Eu vou aceitar o seu cadastro.";
	show "Agora deixe me ver se você está apto para isso, deixe me fazer uma pequena entrevista?";
	next;
	show "[Markie]";
	show "Oh, você naum preciza ficar nervoso com isso morzinho.... Eu só quero saber o quanto você sabe sobre os Arruaceiros.";
	show "Eu faço isso com todas os candidatos. Podemos começar?";
	next;
	menu "Ok.",L_Test1, "Espere... Eu preciso de um pouco mais de tempo.",-;

		show "[Markie]";
		show "Ok docinho, não precisa ter pressa. Quando você estiver pronto basta me falar.";
		close;


//=================================================================
L_Test1:
//=======
	show "[Markie]";
	show "Eu vou te fazer umas perguntas. Ouça bem e responda aquilo que você achar melhor, OK?";
	next;	

	savepoint "in_rogue",370,115;
	set @score, 0;
	set ROGUE_Q, 1;
	if(rand(2)) goto L_QSet2;

		show "[Markie]";
		show "1) Quanto é adicionado à taxa de esquiva que um Gatuno recebe quando ^5533FFaprimora^000000 a habilidade ^5533FFPerícia em Esquiva^000000?";
		next;
		menu "20",sM1_1, "30",-, "40",sM1_1, "160",sM1_1;

			set @score, @score + 10;

		sM1_1:

		show "[Markie]";
		show "2) Escolha um monstro qu não pode detectar um personagem durante Esconderijo ou Furtividade?";
		next;
		menu "Múmia",sM1_2, "Rabo-de-Verme",-, "Argos",sM1_2, "Esqueleto Soldado",sM1_2;

			set @score, @score + 10;

		sM1_2:

		show "[Markie]";
		show "3) Onde se localiza a guilda dos Arruaceiros?";
		next;
		menu "Comodo",sM1_3, "Praia de Cocomo",sM1_3, "Pharos, Ilha do Farol",-, "Morroc",sM1_3;

			set @score, @score + 10;

		sM1_3:

		show "[Markie]";
		show "4) Em qual das seguintes cidades, é possível se tornar um Gatuno?";
		next;
		menu "Comodo",sM1_4, "Lutie",sM1_4, "Alberta",sM1_4, "Morroc",-;

			set @score, @score + 10;

		sM1_4:

		show "[Markie]";
		show "5) Escolha a carta que ^FF3355não^000000 afeta o ^5533FFatributo DES^000000 de um personagem...";
		next;
		menu "Carta Rocker",sM1_5, "Carta Múmia",-, "Carta Zerom",sM1_5, "Carta Drops",sM1_5;

			set @score, @score + 10;

		sM1_5:

		show "[Markie]";
		show "6) O que você acha legal em ser um Arruaceiro?";
		next;
		menu "Ser um vagabundo",sM1_6, "Ser um hipócrita",sM1_6, "Ser um sem vergonha",sM1_6, "Ter uma excelente força de ataque",-;

			set @score, @score + 10;

		sM1_6:

		show "[Markie]";
		show "7) Em qual nível de classe um Gatuno pode se tornar um Arruaceiro?";
		next;
		menu "Nível 30",sM1_7, "Nível 35",sM1_7, "Nível 40",-, "Nível 50",-;

			set @score, @score + 10;

		sM1_7:

		show "[Markie]";
		show "8) Se você quiser colorir seu cabelo de uma cor diferente, onde você deve ir para fazer isto?";
		next;
		menu 	"Construção ao Sudoeste de Morroc",sM1_8, "Construção ao Sudoeste de  Prontera",-,
			"Construção ao Sudeste de  Morroc",sM1_8, "Construção ao Nordeste de Prontera",sM1_8;

			set @score, @score + 10;

		sM1_8:

		show "[Markie]";
		show "9) Quais cogumelos você precisa furtar para se tornar um Gatuno?";
		next;
		menu "Cogumelos Borrachentos",-, "Cogumelos vermelhos",sM1_9, "Cogumelos Venenosos",-, "Cogumelos Pegajosos",sM1_9;

			set @score, @score + 10;

		sM1_9:

		show "[Markie]";
		show "10) Quais dessas cartas são ^FF3355inúteis^000000 para um Arruaceiro?";
		next;
		menu "Carta Sussurro",sM1_10, "Carta Salgueiro Ancião",-, "Carta Zerom",sM1_10, "Carta Matyr",sM1_10;

			set @score, @score + 10;

		sM1_10:
		goto L_Score;


	L_QSet2:
		show "[Markie]";
		show "1) Qual habilidade você deve aprender antes de aprender ^5533FFTúnel de Fuga^000000?";
		next;
		menu "Esconderijo",-, "Furto",sM2_1, "Perícia em Esquiva",sM2_1, "Golpe Fulminante",sM2_1;

			set @score, @score + 10;

		sM2_1:

		show "[Markie]";
		show "2) Quanto de disconto um Arruaceiro pode conseguir com ^5533FFExtorquir^000000 a mais que a habilidade de Mercador, ^5533FFDisconto^000000?";
		next;
		menu "3%",sM2_2, "2%",sM2_2, "1%",-, "0%",sM2_2;

			set @score, @score + 10;

		sM2_2:

		show "[Markie]";
		show "3) Qual é a descrição correta da habilidade ^5533FFAfanar^000000?";
		next;
		menu "Furtar itens de jogadores",sM2_3, "Furtar itens de monstros",sM2_3, "Furtar Zeny de monstros",-, "Furtat Zeny de jogadores",sM2_3;

			set @score, @score + 10;

		sM2_3:

		show "[Markie]";
		show "4) Quantos Arruaceiros são necessários para ativar a habilidade ^5533FFMalandragem^000000?";
		next;
		menu "1 Arruacerio + 2 Mercenários",sM2_4, "2 Arruaceiros + 1 Gatuno",sM2_4, "4 Gatunos",sM2_4, "2 ou + Arruaceiros",-;

			set @score, @score + 10;

		sM2_4:

		show "[Markie]";
		show "5) Depois de aumentar ^5533FFRemover Capacete^000000 para o nível 5, qual outra habilidade se tornará possível aprender?";
		next;
		menu "Envenenar",sM2_5, "Strip Tease",sM2_5, "Explosão Venenosa",sM2_5, "Remover Escudo",-;

			set @score, @score + 10;

		sM2_5:

		show "[Markie]";
		show "6) Escolha a habilidade que deixa você mover enquanto você permanece oculto?";
		next;
		menu "Esconderijo",sM2_6, "Recuar",sM2_6, "Tunel de Fuga",-, "Chutar Areia",sM2_6;

			set @score, @score + 10;

		sM2_6:

		show "[Markie]";
		show "7) Escolha a carta que aumenta a ^5533FFtaxa de precião^000000 ao seu dono?";
		next;
		menu "Andre",sM2_7, "Familiar",sM2_7, "Mumia",-, "Marina",sM2_7;

			set @score, @score + 10;

		sM2_7:

		show "[Markie]";
		show "8) Escolha o monstro que recebe mais dano quando atacado com uma arma com Carta Vadon?";
		show "(Carta Vadon influencia 20% mais dano em monstro do elemento fogo)";
		next;
		menu 	"Vadon",sM2_8, "Deviruchi",sM2_8, "Salgueiro Ancião",-, "Bafomé",sM2_8;

			set @score, @score + 10;

		sM2_8:

		show "[Markie]";
		show "9) Quanto de SP o ^5533FFAtaque Duplo^000000 precisa quando usado com uma adaga?";
		next;
		menu "15 SP",sM2_9, "Não precisa de SP",-, "10 SP",sM2_9, "54 SP",sM2_9;

			set @score, @score + 10;

		sM2_9:

		show "[Markie]";
		show "10) Escolha a melhor adaga para ser usada no Tunel Submarino?";
		next;
		menu "Main-Gauche Trovejante",-, "Main-Gauche Glacial",sM2_10, "Main-Gauche Terrestre",sM2_10, "Main-Gauche Flamejante",sM2_10;

			set @score, @score + 10;

		sM2_10:


	L_Score:
		show "[Markie]";
		show "Tudo bem, essas foram todas as perguntas que eu precisava saber.  Agora não foi tão ruim né?";
		//show "I hope you don't mind, but I've been grading your answers.";
		next;
		show "[Markie]";
		show "Eu gosto de dar a pontuação para as pessoas para elas saberem seus pontos.";
		show "Parece que você conseguiu ^FF3355" + @score +"^000000/100 pontos............";
		next;
		show "[Markie]";

		if(@score< 90) goto sL_Failed;
		if(@score<100) goto sL_Passed;

		sL_Perfect:
			show "Uma prova perfeita! Eu sabia que você tinha o que era preciso para ser um Arruaceiro.....";
			show "Mas não deixe que isso te suba a cabeça, amor, por que ainda tem muitos testes te aguardando.....";
			next;
			show "[Markie]";
			set ROGUE_Q, 2;
			goto L_Test2;

		sL_Passed:
			show "Bom, muito bom. Você está no trabalho certo, docinho. Mas ainda tem muitos testes pra você fazer então não fique exitado ainda.....";
			next;
			show "[Markie]";
			set ROGUE_Q, 2;
			goto L_Test2;
			return;

		sL_Failed:
			show "..... hum... bem. Este não é o tipo de ponto que eu estava esperando......";
			show "O que eu posso dizer, com um ponto como esse você definitivamente não pode ser um Arruaceiro..........";
			emotion e_swt;
			next;
			show "[Markie]";
			show "Por que você não volta depois de treinar um pouco, amor..... você precisa..... ";
			close;

L_ReTest:
	show "Você de novo? Você tem certeza que está pronto desta vez?...... Ok docinho, então relaxe....";
	next;
	goto L_Test1;

L_Test2:
	show "Seu segundo teste lhe será aplicado pelo ^5533FFSr. Smith^000000.";
	show "Eu tenho que avisa-lo, Sr. Smith pode ser uma pessoa rígida e dura de se lidar. Então, seja cuidadosa com as suas ações com ele.";
	close;
L_Test3:
	show "Eu sei que esses caras são difícies de se achar. tenha paciência e continue procurando. Tenha ceteza que você tem todas as corretas senhas tambêm..";
	close;
L_Test4:
	show "O que você está fazendo aqui? Eu espero que você naum esteja tentando fugir do último teste, você está?.........";
	emotion e_what;
	next;
	show "[Markie]";
	show "Você quase acabou todos, então continue tentando. Eu sei que você consegue.";
	close;


//=======================================================================
L_Change:
//===========
	show "Oh, você voltou e ainda está inteiro. Você aparentemente passou em todos os testes docinho.";
	show "Você provou que está pronto para se tornar um Arruaceiro!";
	emotion e_ic;
	next;
	if(SkillPoint != 0) goto L_SkillP;
	show "[Markie]";
	show "Ta-da! Meus parabêns por se tornar um Arruaceiro. Você se mostrou muito capaz e está é a sua recompensa por isso......";
	setlook 7,0;
	callfunc "Job_Change",Job_Rogue;
	emotion e_grat;
	next;
	show "[Markie]";
	show "Agora que você você é um Arruaceiro, você está pronto para cair no mundo e seguir viagem.";
	if(JBLVL != 50) {
			additem 1219,1;		// 2 slott gladius
		} else {
			additem 1220,1;		// 3 slott gladius
	}
	callfunc "F_ClearJobVar";
	next;
	show "[Markie]";
	show "Só tenha em mente que esta liberdade requer responsabilidade. Trate bem as pessoas que você encontrar bem e com respeito.";
	next;
	show "[Markie]";
	show "Divirta-se baby! Vejo você por aí!";
	close;

//=======================================================================
L_SkillP:
//===========
	show "[Markie]";
	show "Oh! Bem... uma coisa mor... você ainda tem pontos de habilidade para distribuir!";
	show "Por favor, os distribua e depois volte aqui, está bem?";
	close;
}


// Mr. Smith: Tests 2 and 3
in_rogue,376,23,3	script	Mr. Smith	57,{

	show "[Mr. Smith]";
	if(BaseJob == Job_Thief) goto L_Thief;
	if(BaseJob == Job_Rogue) goto L_Rogue;

L_OtherJob:
	show "Suma! Estou ocupado.....";
	close;
L_Rogue:
	show "Procurando por seus amigos, Arruaceiro.";
	close;
L_Thief:
	if(ROGUE_Q == 2) goto L_Test2;
	if(ROGUE_Q == 3) goto L_Test3;
	if(ROGUE_Q > 3) goto L_Done;
	show "Ok, ok.... Um... Dois.... Três.... Onde você faz os seus negócios... onde é.... Morroc... Geffen.... Comodo??.......";
	next;
	show "[Mr. Smith]";
	show "Uhh..... isto está me dando dor de cabeça.......";
	next;
	show "[Mr. Smith]";
	show "Huh? Quem é você? Se você quer se tornar um Arruaceiro, vá e fale com ^5533FFMarkie^000000....";
	next;
	show "[Mr. Smith]";
	show "...... Droga! Eu esqueci o que eu estava contando!!";
	emotion e_wah;
	close;

//----------------------------------------------------------------------------------------------------
L_Test2:
//--------------
	if(ROGUE_Q2 > 0) goto L_Check2;
	show "Então você veio para o segundo teste.... bom.......";
	next;
	show "[Mr. Smith]";
	show "Antes de começarmos, deixe me falar sobre o teste em si.";
	show "A nossa guild está envolvidas em muitos 'negocios' e sempre nós precisamos de fundos.";
	next;
	show "[Mr. Smith]";
	show "Por esse motivo, nós precisamos de ^5533FF10,000 zeny^000000 a ser pago para você fazer o teste de Arruaceiro.";
	show "O pagamento será coletado quando você acabar esse segundo teste, então não se preocupe se você não tem isso agora.";
	next;
	show "[Mr. Smith]";
	show "Bem, agora o teste..... Você é necessário para coletar os seguintes itens:";
	next;
	show "[Mr. Smith]";
	set ROGUE_Q2, rand(1,3);
	if(ROGUE_Q2 == 1) callsub sF2_R1;
	if(ROGUE_Q2 == 2) callsub sF2_R2;
	if(ROGUE_Q2 == 3) callsub sF2_R3;
	next;
	show "[Mr. Smith]";
	show "Uma vez que você tenha TODOS os itens e os 10,000 zeny, venha me ver de novo, OK? Bom.";
	close;

	sF2_R1:
		show "^5533FF6 Ervas Azu,";
		show "10 Ossos,";
		show "10 Unhas Apodrecidas,";
		show "10 Mandíbulas Horrendas^000000.";
		return;
	sF2_R2:
		show "^5533FF10 Ervas Verde,";
		show "10 Garletas,";
		show "10 Escamas de Cobra";
		show "10 Cascos de Caranguejo.^000000";
		return;
	sF2_R3:
		show "^5533FF10 Ervas Amarela,";
		show "10 Patas de Urso,";
		show "10 Cascas";
		show "10 Pernas de Gafanhoto^000000.";
		return;

L_Check2:
	set @count, 0;
	if(ROGUE_Q2 == 2) goto L_2;
	if(ROGUE_Q2 == 3) goto L_3;

	L_1:
		set @R, 1;
		if(countitem(510)<6 || countitem(932)<10 || countitem(957)<10 || countitem(958)<10 || Zeny < 10000) goto L_NotEnuf;
		delitem 510,6;
		delitem 932,10;
		delitem 957,10;
		delitem 958,10;
		goto L_Done2;

	L_2:
		set @R, 2;
		if(countitem(511)<10 || countitem(910)<10 || countitem(926)<10 || countitem(964)<10 || Zeny < 10000) goto L_NotEnuf;
		delitem 511,10;
		delitem 910,10;
		delitem 926,10;
		delitem 964,10;
		goto L_Done2;

	L_3:
		set @R, 3;
		if(countitem(508)<10 || countitem(948)<10 || countitem(935)<10 || countitem(940)<10 || Zeny < 10000) goto L_NotEnuf;
		delitem 508,10;
		delitem 948,10;
		delitem 935,10;
		delitem 940,10;

	L_Done2:
		show "Deixe me ver.... matricula.... checando..... itens....... checando...... Tudu bem, tudu parece em ordem.  Você passou no segundo teste.";
		next;
		show "[Mr. Smith]";
		show "Quando você estiver pronto para o próximo teste, me avise. OK?";
		set Zeny, Zeny - 10000;
		set ROGUE_Q, 3;
		set ROGUE_Q2, 0;
		close;

	L_NotEnuf:
		show ".... Mais que Inferno!?!? Você não tem todos os itens? Então por que você voltou?? Não me diga que você já esqueceu o que era.....";
		emotion e_what;
		next;
		show "[Mr. Smith]";
		show "Desta vez escreva! Você precisa de ^5533FF10,000^000000 zeny e:";
		if(ROGUE_Q2 == 1) callsub sF2_R1;
		if(ROGUE_Q2 == 2) callsub sF2_R2;
		if(ROGUE_Q2 == 3) callsub sF2_R3;
		next;
		show "[Mr. Smith]";
		show "Anotou? NÃO VOLTE até você ter tudu.........";
		emotion e_pif;
		close;


//----------------------------------------------------------------------------------------------
L_Test3:
//---------------
	if(ROGUE_Q2 > 0) goto L_Check3;
	show "Deixe me ver..... Quem eu tinha que encontrar........ hmm.........";
	emotion e_hmm;
	next;
	show "[Mr. Smith]";
	show "Ah! Lembrei! Tudu bem, no teste anterior você tinha que encontrar algum itens. Neste teste você precisa encontrar uma pessoa específica.";
	emotion e_ic;
	next;
	show "[Mr. Smith]";
	set ROGUE_Q2, rand(1,3);
	if(ROGUE_Q2 == 1) callsub sF3_R1;
	if(ROGUE_Q2 == 2) callsub sF3_R2;
	if(ROGUE_Q2 == 3) callsub sF3_R3;
	next;
	show "[Mr. Smith]";
	show "Boa sorte para você. Tente acabar o teste o mais rapido possível. Obter coisas rapido é um dos desafios de um Arruaceiro.";
	close;

	sF3_R1:
		show "Você precisa encontrar ^5533FFAragham Junior^000000.";
		next;
		show "[Mr. Smith]";
		show "Ele mora em uma pequena casa em algum lugar a ^5533FFSudoeste^000000 da Fortaleza de Sandarman.";
		show "Essa área é localizada ^5533FFum mapa a leste^000000 daqui e é no ^000000banco de terra ao sudoeste^000000.";
		next;
		show "[Mr. Smith]";
		show "Ele é uma pessoa muito boa que trabalha verdadeiramente duro, e ele é muito bom coletando dividas não pagas.";
		show "Infelizmente, pessoas aqui estão tentando mata-lo pelo que o pai dele fez no pasado.";
		next;
		show "[Mr. Smith]";
		show "Quando ele se juntou a Guilda dos Arruaceiros, ele decidiu que ele ficaria escondidu até que a poeira baixasse por isso ele está escondido.";
		show "Ninguêm sabe onde ele está, somente nós que somos da Guilda";
		next;
		show "[Mr. Smith]";
		show "Por isso que você precisa da senha para falar com ele.";
		show "A senha é: '^5533FFAragham nunca recebeu itens.^000000'";
		return;
	sF3_R2:
		show "Você precisa encontrar ^5533FFHollgrehen Junior^000000.";
		next;
		show "[Mr. Smith]";
		show "Ele mora em uma pequena casa em algum lugar a ^5533FFSudeste^000000 da Forteleza de Sandarman.";
		show "Essa área é localizada ^5533FFum mapa a leste^000000 daqui e ele está em ^5533FFbanco de terra à leste^000000.";
		next;
		show "[Mr. Smith]";
		show "Ele é uma pessoa muito boa que trabalha verdadeiramente duro, e ele é muito bom coletando dividas não pagas.";
		show "Infelizmente, pessoas aqui estão tentando matá-lo pelo que o pai dele fez no pasado.";
		next;
		show "[Mr. Smith]";
		show "Quando ele se juntou a Guilda dos Arruaceiros, ele decidiu que ele ficaria escondidu até que a poeira baixasse por isso ele está escondido.";
		show "Ninguêm sabe onde ele está, somente nós que somos da Guilda.";
		next;
		show "[Mr. Smith]";
		show "Por isso que você precisa da senha para falar com ele.";
		show "A senha é: '^5533FFMeu pai nunca recebeu itens.^000000'";
		return;
	sF3_R3:
		show "Você precisa ir e encontrar ^5533FFAntonio Junior.^000000.";
		next;
		show "[Mr. Smith]";
		show "Ele mora em uma pequena cabana em algum lugar de ^5533FFNo Leste, no fim da Praia de Comodo^000000.";
		show "Essa área está localizada ^5533FF1 mapa ao Norte, e 1 mapa Oeste^000000 daqui.";
		next;
		show "[Mr. Smith]";
		show "Ele é uma pessoa muito boa que trabalha verdadeiramente duro, e ele é muito bom coletando dividas não pagas.";
		show "Infelizmente, pessoas aqui estão tentando mata-lo pelo que o pai dele fez no pasado.";
		next;
		show "[Mr. Smith]";
		show "Quando ele se juntou a Guilda dos Arruaceiros, ele decidiu que ele ficaria escondidu até que a poeira baixasse.";
		show "Ninguêm sabe onde ele está, somente nós que somos da Guilda.";
		next;
		show "[Mr. Smith]";
		show "Por isso que você precisa da senha para falar com ele.";
		show "A senha é: '^5533FFAntonio não gosta de quebrar material refinado.^000000'";
		return;


L_Check3:
	show "O que!?! Você esqueceu onde você tinha que ir?........";
	emotion e_what;
	next;
	show "[Mr. Smith]";
	show "GRRRR!!! Por isso que você tem que escrever as coisas que são importantes! Jees......";
	emotion e_an;
	next;
	show "[Mr. Smith]";
	if(ROGUE_Q2 == 1) callsub sF3_R1;
	if(ROGUE_Q2 == 2) callsub sF3_R2;
	if(ROGUE_Q2 == 3) callsub sF3_R3;
	close;

//-------------------------------------------------------------------------------------------------
L_Done:
//----------------
	show "Eu não tenho mais nada para você.........";
	close;
}

// Hermanthorn Jr.
in_rogue,272,136,3	script	Hermanthorn Jr.	85,{
	show "[Hermanthorn Jr.]";
	show "Huh? O qu.... Quem é você?! Você não é da Guilda dos Arruaceiros!! Some da minha frente!";
	emotion e_what;
	next;
	show "[Hermanthorn Jr.]";
	show "Você é..... Entendo! Você é um deles!!! Você veio pra me matar, não é??!! Não... NÃO!! Eu não estou pronto pra morrer!";
	emotion e_gasp;
	next;
	show "[Hermanthorn Jr.]";
	show "Aaaaaaaaaaaaaahhhh!! vai embora! Some daqui! Do contrário eu vou MATAR VOCÊ!!!";
	emotion e_wah;
	close;
}

// --------------------------------------------------------------------------
cmd_fild09,106,195,0	script	Aragham Junior#01	45,1,1,{
	callfunc "F_RogueTest3", 1, "Aragham", "nunca", "recebeu", "itens.", 244, 24;
}

// -----------------------------------------------------------------------------
cmd_fild09,335,143,0	script	Hollgrehen Junior#01	45,1,1,{
	callfunc "F_RogueTest3", 2, "Meu pai", "nunca", "recebeu", "itens.", 168, 34;
}

// ------------------------------------------------------------------------------
cmd_fild04,304,179,0	script	Antonio Junior#01	45,1,1,{
	callfunc "F_RogueTest3", 3, "Antonio", "não gosta", "de quebrar", "material refinado.", 165, 104;
}

// Function for Guildsman ===================================
function	script	F_RogueTest3	{

	show "[???]";
	show "Quem esta ai?!!!";
	show "Quem ousa entrar no meu território?";
	if(god_megin_2 > 0 && getarg(1) == "Aragham") goto L_Pass;
	if(BaseJob!=Job_Thief || ROGUE_Q<3 || ROGUE_Q2==0) close;
L_Pass:
	deletearray @choice$[1], 4;
	next;
	menu "Meu pai",M_1a, "Aragham",M_1b, "Antonio",M_1c, "Legolas",M_1d;

	M_1a:
		set @choice$[1], "Meu pai";
		goto M_Menu2;
	M_1b:
		set @choice$[1], "Aragham";
		goto M_Menu2;
	M_1c:
		set @choice$[1], "Antonio";
		goto M_Menu2;
	M_1d:
		set @choice$[1], "Legolas";

	M_Menu2:
	callsub sF_Password;
	menu "não faz",M_2a, "não gosta",M_2b, "nunca",M_2c, "sempre",M_2d;

	M_2a:
		set @choice$[2], "não faz";
		goto M_Menu3;
	M_2b:
		set @choice$[2], "não gosta";
		goto M_Menu3;
	M_2c:
		set @choice$[2], "nunca";
		goto M_Menu3;
	M_2d:
		set @choice$[2], "sempre";

	M_Menu3:
	callsub sF_Password;
	menu "recebe",M_3a, "recebeu",M_3b, "se escondeu",M_3c, "examinou",M_3d, "de quebrar",M_3e;

	M_3a:
		set @choice$[3], "recebe";
		goto M_Menu4;
	M_3b:
		set @choice$[3], "recebeu";
		goto M_Menu4;
	M_3c:
		set @choice$[3], "se escondeu";
		goto M_Menu4;
	M_3d:
		set @choice$[3], "examinou";
		goto M_Menu4;
	M_3e:
		set @choice$[3], "de quebrar";

	M_Menu4:
	callsub sF_Password;
	menu "itens.",M_4a, "itens roubados.",M_4b, "material refinado.",M_4c, "equipamento.",M_4d;

	M_4a:
		set @choice$[4], "itens.";
		goto L_Check;
	M_4b:
		set @choice$[4], "itens roubados.";
		goto L_Check;
	M_4c:
		set @choice$[4], "material refinado.";
		goto L_Check;
	M_4d:
		set @choice$[4], "equipamento.";

L_Check:
	callsub sF_Password;
	if(god_megin_2 > 0) goto L_Enter;
	if(ROGUE_Q2 != getarg(0)) goto L_Wrong;
L_Enter:
//	if((@choice$[1] != getarg(1)) || (@choice$[2] != getarg(2)) || (@choice$[3] != getarg(3)) || (@choice$[4] != getarg(4))) goto L_Wrong;
	show "~ weeerk ~";
	show "Você ouve a porta começando a se abrir........";
	next;
	deletearray @choice$[1], 4;
	warp "in_rogue", getarg(5), getarg(6);
	end;

	L_Wrong:
	show "~ weeerk ~";
	show "Você ouve a porta começando a se abrir........";
	next;
	deletearray @choice$[1], 4;
	warp "in_rogue", getarg(5), getarg(6);
	end;
sF_Password:
	show "[^5533FF" + strcharinfo(0) + "^000000]";
	show @choice$[1];
	show @choice$[2];
	show @choice$[3];
	show @choice$[4];
	next;
	return;
}


//=======================================================================================================//
//						Test 4: Last Test						//
//***********************************************************************************************************************************************************//
// Other path
// warp "in_rogue", 11, 388;
// 390,389 end warp
// 9,9 -> 9,21
//===============================================
// Guildsman
//===============================================
//---------------------------------------------------------------------------
in_rogue,244,39,4	script	Aragham Junior#02	99,{
	callfunc "F_RogueTest4", 1, "Aragham Junior", 245, 27;
}
//---------------------------------------------------------------------------
in_rogue,152,27,6	script	Hollgrehen Junior#02	85,{
	callfunc "F_RogueTest4", 2, "Hollgrehen Junior", 160, 32;
}
//---------------------------------------------------------------------------
in_rogue,183,105,4	script	Antonio Junior#02	88,{
	callfunc "F_RogueTest4", 3, "Antonio Junior", 175, 107;
}

// Function for Guildsman =============================
function	script	F_RogueTest4	{

	if(BaseJob != Job_Thief || ROGUE_Q2 != getarg(0)) goto L_NoThief;
	if(ROGUE_Q == 4) goto L_Restart;
	show "[" + getarg(1) + "]";
	show "Olááá... Você deve ser da Guilda dos Arruaceiros. Meu nome é " + getarg(0) + "...... Eu sou o Arruaceiro do Deserto..............";
	next;
	show "[" + getarg(1) + "]";
	show "Aww a quem eu estou enganando...... Vamos cortar o papo furado.  Então você quer se tornar um Arruaceiro?";
	emotion e_swt;
	next;
	show "[" + getarg(1) + "]";
	show "Como você sabe, o mandamento dos Arruaceiros é...... ^5533FF'Escape dos fortes! Seja malísioso com os fracos!'^000000";
	show "Está regra simples pode ser aplicada lutando com monstros tambêm.";
	next;
	show "[" + getarg(1) + "]";
	show "A última coisa que você tem que fazer para se tornar um Arruaceiro, é andar através da pasagem subterrânia até a Guilda.";
	next;
	show "[" + getarg(1) + "]";
	show "Parece facil né? Bem, não é! Mas não se preocupe....";
	show "Se você se mantiver firme ao mandamento dos Arruaceiro, se esconder , e correr muito, você ficará bem.";

	M_Menu:
	next;
	show "[" + getarg(1) + "]";
	show "Ok, você está pronto pra ir?";
	next;
	menu "Como nunca estive antes.",-, "Na verdade, Estou um pouco assustado...",M_End;

		show "[" + getarg(1) + "]";
		show "Boa sorte então, você vai precizar....";
		next;
		set ROGUE_Q, 4;
		killmonsterall "in_rogue";
		if (ROGUE_Q2 == 1) savepoint "in_rogue", 245, 29;
		if (ROGUE_Q2 == 2) savepoint "in_rogue", 161, 34;
		if (ROGUE_Q2 == 3) savepoint "in_rogue", 166, 107;
		warp "in_rogue", 15, 105;
		end;

	M_End:
		show "[" + getarg(1) + "]";
		show ".....................";
		next;
		show "[" + getarg(1) + "]";
		show "Bem, você tem todo o tempo do mundo...... Eu acho.......";
		close;

L_Restart:
	show "[" + getarg(1) + "]";
	show "....... Parece que você foi pego.......";
	emotion e_swt;
	next;
	show "[" + getarg(1) + "]";
	show "Se você quizer, eu te mando de volta.  Uma falha é só um dos caminhos errados do sucesso.... yada yada yada....";
	percentheal 100,100;
	goto M_Menu;

L_NoThief:
	show "[" + getarg(1) +"]";
	show "Eu não sei como você conseguiu este";
		if(ROGUE_Q2)
	{
		show "passe. Mas eu não posso";
		show "fazer anda por você.";
	}
		else
	{
		show "passe, mas olhando sua cara,";
		show "Eu suponho que você não estja aqui para o";
		show "teste de Arruaceiro.";
	}
	next;
	show "[" + getarg(1) +"]";
	show "Por favor, se apresse e saia";
	show "deste lugar assim que você puder!";
	close;
}

//================================================
// Monster Spawns
//================================================
in_rogue,15,185,0	script	rogue_mob1	139,8,0,{
	monster "in_rogue",14,188,"Zumbi",1015,1;
	monster "in_rogue",15,188,"Zumbi",1015,1;
	monster "in_rogue",16,188,"Zumbi",1015,1;
	monster "in_rogue",14,189,"Zumbi",1015,1;
	monster "in_rogue",15,189,"Zumbi",1015,1;
	monster "in_rogue",16,189,"Zumbi",1015,1;
	end;
}
in_rogue,15,245,0	script	rogue_mob2	139,8,0,{
	monster "in_rogue",14,247,"Múmia",1041,1;
	monster "in_rogue",15,247,"Múmia",1041,1;
	monster "in_rogue",16,247,"Múmia",1041,1;
	monster "in_rogue",14,248,"Múmia",1041,1;
	monster "in_rogue",15,248,"Múmia",1041,1;
	monster "in_rogue",16,248,"Múmia",1041,1;
	end;
}
in_rogue,15,328,0	script	rogue_mob3	139,8,0,{
	monster "in_rogue",14,331,"Zumbi",1015,1;
	monster "in_rogue",15,331,"Zumbi",1015,1;
	monster "in_rogue",16,331,"Zumbi",1015,1;
	monster "in_rogue",14,332,"Zumbi",1015,1;
	monster "in_rogue",15,332,"Zumbi",1015,1;
	monster "in_rogue",16,332,"Zumbi",1015,1;
	end;
}
in_rogue,35,342,0	script	rogue_mob4	139,0,8,{
	monster "in_rogue",38,340,"Múmia",1041,1;
	monster "in_rogue",37,342,"Cavaleiro do Abismo",1219,1;
	monster "in_rogue",39,342,"Carniçal",1036,1;
	monster "in_rogue",38,344,"Múmia",1041,1;
	end;
}
in_rogue,57,306,0	script	rogue_mob5	139,4,0,{
	monster "in_rogue",59,301,"Khalitzburg",1132,1;
	end;
}
in_rogue,96,333,0	script	rogue_mob6	139,0,4,{
	monster "in_rogue",92,325,"Khalitzburg",1132,1;
	end;
}
in_rogue,139,313,0	script	rogue_mob7	139,4,0,{
	monster "in_rogue",139,309,"Cavaleiro do Abismo",1219,1;
	end;
}
in_rogue,135,246,0	script	rogue_mob8	139,0,4,{
	monster "in_rogue",139,245,"Zumbi",1015,2;
	monster "in_rogue",139,246,"Zumbi",1015,2;
	monster "in_rogue",139,247,"Zumbi",1015,2;
	end;
}
in_rogue,62,302,0	script	rogue_mob9	139,0,4,{
	monster "in_rogue",57,305,"Khalitzburg",1132,1;
	end;
}
in_rogue,199,218,0	script	rogue_mob10	139,0,8,{
	monster "in_rogue",157,218,"Zumbi",1015,10;
	monster "in_rogue",157,218,"Cavaleiro do Abismo",1219,1;
	monster "in_rogue",157,218,"Carniçal",1036,1;
	monster "in_rogue",157,218,"Esqueleto Arqueiro",1016,1;
	end;
}
in_rogue,86,187,0	script	rogue_mob11	139,0,8,{
	monster "in_rogue",88,186,"Múmia",1041,1;
	monster "in_rogue",88,187,"Múmia",1041,1;
	monster "in_rogue",88,188,"Múmia",1041,1;
	end;
}
in_rogue,225,187,0	script	rogue_mob12	139,0,8,{
	monster "in_rogue",232,191,"Cavaleiro do Abismo",1219,1;
	monster "in_rogue",233,190,"Cavaleiro do Abismo",1219,1;
	monster "in_rogue",234,189,"Cavaleiro do Abismo",1219,1;
	end;
}
in_rogue,252,320,0	script	rogue_mob13	139,0,8,{
	monster "in_rogue",252,319,"Múmia",1041,1;
	monster "in_rogue",252,321,"Múmia",1041,1;
	monster "in_rogue",252,319,"Carniçal",1036,1;
	monster "in_rogue",252,321,"Carniçal",1036,1;
	monster "in_rogue",257,320,"Esqueleto Arqueiro",1016,1;
	monster "in_rogue",248,320,"Esqueleto Arqueiro",1016,1;
	end;
}

//================================================
// End Warp
//================================================
in_rogue,370,320,0	script	rogue04	45,1,1,{
	set ROGUE_Q, 5;
	killmonsterall "in_rogue";
	warp "in_rogue",378,113;
	savepoint "in_rogue",370,115;
	end;
}

//================================================
// Timer: Keeps monsters from overspawning
//================================================
-	script	RogueTest3	-1,{
	end;

OnMinute10:
OnMinute20:
OnMinute30:
OnMinute40:
OnMinute50:
OnMinute60:
	killmonsterall "in_rogue";
	end;
}

//==============================================================================
// mapflag
//==============================================================================
in_rogue	mapflag	nomemo
in_rogue	mapflag	noteleport
in_rogue	mapflag	nosave	SavePoint
in_rogue	mapflag	nopenalty
in_rogue	mapflag	nobranch
in_rogue	mapflag	noexp
in_rogue	mapflag	noloot
