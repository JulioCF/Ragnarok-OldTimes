//= Cronus r24xx + otRag ??

// Doll Quest
alberta,117,134,8	script	Elin	96,{
	mes "[Elin]";
	if (Sex)
	{
		mes "Olá, senhor.";
		mes "Hmm, como você é um garoto,";
		mes "você não gosta de bonecas certo?";
	}
	else
	{
		mes "Olá, senhorita";
		mes "Como você é uma garota,";
		mes "você deve gostar de bonecos, certo?";
	}
	next;
	mes "[Elin]";
	mes "Eu AMO bonecas!!";
	emotion e_lv2;
	next;
	mes "[Elin]";
	mes "Você sabe..... Eu REALMENTE,";
	mes "REALMENTE, quero uma boneca";
	mes "nova. Espero que papai vá me";
	mes "comprar uma para o meu aniversário.....";
	next;
	if(select("Ahnn, você deve ser feliz...:E que tal eu te dar uma agora?")==1){
		mes "[Elin]";
		mes "É, essa vez eu estou";
		mes "torcendo por um Boneco de Yoyo.";
		mes "Hehe, eles são tão fofos! E são meus FAVORITOS!";
		emotion e_ok;
		close;
	}
	mes "[Elin]";
	mes "Oh meu Deus! Sério??";
	mes "Você é um amor….me dá, me dá!!";
	mes "Que tipo de boneca você vai me dar?";
	mes "Você realmente vai me dar uma?";
	emotion e_gasp;
	next;
	switch(select("Bengala Doce:Boneco de Poring:Boneco de Chonchon:Boneco de Rocker:Boneco de Esporo:Boneco de Osiris:Boneco de Bafomé:Boneco de Fumacento:Boneco de Yoyo:Eu sou tão adorável quanto uma boneca!")){
		case 1:
			if(countitem(740) > 0){
				delitem 740,1;
				mes "[Elin]";
				mes "Nossa! É um coelho!";
				mes "É TÃOOO fofo!! MUITO obrigada a você!";
				emotion e_lv2;
				next;
				mes "[Elin]";
				mes "É TÃOO fofinho... Ah sim!";
				mes "Deixe-me dar um presente para você também!";
				next;
				mes "(Procura nos bolsos dela)";
				next;
				mes "[Elin]";
				mes "Hummm ... achei! Aqui, aceite isso!";
				next;
				getitem 530,1;
				mes "[Elin]";
				mes "Eu ganhei isso do papai noel.";
				mes "É uma bengala doce~";
				next;
				mes "[Elin]";
				mes "Obrigada novamente pela boneca coelho!";
				mes "Eu vou ABRAÇAR isso para dormir TODA noite~";
				emotion e_thx;
				close;
			}
			break;
		case 2:
			if(countitem(741) > 0){
				delitem 741,1;
				mes "[Elin]";
				mes "Nossa! É uma boneca poring!";
				mes "É TÃOOO fofa! Já que você me deu uma boneca,";
				mes "eu vou lhe dar isso....";
				emotion e_lv2;
				next;
				getitem 529,1;
				mes "[Elin]";
				mes "Obrigada pela boneca!";
				emotion e_thx;
				close;
			}
			break;
		case 3:
			if(countitem(742) > 0){
				delitem 742,1;
				mes "[Elin]";
				mes "Hã o que é isso?.... uma boneca ... chonchon? ...";
				emotion e_wah;
				next;
				mes "[Elin]";
				mes "Ah bem, já que você está";
				mes "dando ela para mim, eu acho que esta ok ....";
				next;
				mes "[Elin]";
				mes "Bem aqui há alguma coisinha";
				mes "por me dar a boneca.  Não coma muito";
				mes "ou você vai ganhar buracos.";
				getitem 530,1;
				next;
				mes "[Elin]";
				mes ".... chonchon.... (~resmungando, resmungando~)";
				emotion e_ag;
				close;
			}
			break;
		case 4:
			if(countitem(752) > 0){
				delitem 752,1;
				mes "[Elin]";
				mes "Oh! uma boneca Rocker.";
				mes "Eu odeio coisas pulantes";
				mes "mas essa boneca é até fofa.";
				next;
				mes "[Elin]";
				mes "Obrigada. Deixe-me lhe dar algo tambem.";
				next;
				mes "[Elin]";
				mes "Hmm, eu não peguei nada do meu tio...";
				next;
				mes "[Elin]";
				mes "Aqui está. Eu peguei toneladas";
				mes "disso em casa então vou repartirum pouco com você~";
				getitem 532,7;
				next;
				mes "[Elin]";
				mes "Obrigada novamente pela boneca!";
				emotion e_thx;
				close;
			}
			break;
		case 5:
			if(countitem(743) > 0){
				delitem 743,1;
				mes "[Elin]";
				mes "Bleargh .... É um cogumelo... Mamãe sempre me repreede para que eu não coma cogumelos....";
				emotion e_swt;
				next;
				mes "[Elin]";
				mes "Eu vou aceitar de qualquer maneira,";
				mes "desde que a boneca parece fofa.";
				mes "Mas eu ainda não vou comer cogumelos no entanto...";
				next;
				mes "[Elin]";
				mes "Hmm, eu vou precisar te dar um presente tambem.";
				next;
				mes "[Elin]";
				mes "Minha mãe fez isso, por favor aceite-o.";
				getitem 538,5;
				next;
				mes "[Elin";
				mes "Obrigada pela boneca~";
				emotion e_thx;
				close;
			}
			break;
		case 6:
			if(countitem(751) > 0){
				delitem 751,1;
				mes "[Elin]";
				mes "Nossa! É uma boneca Osiris!";
				next;
				mes "[Elin]";
				mes "Aqui está algo para você tambem.";
				getitem 522,2;
				mes "[Elin]";
				mes "Obrigada pela boneca!";
				close;
			}
			break;
		case 7:
			if(countitem(750) > 0){
				delitem 750,1;
				mes "[Elin]";
				mes "Nossa! É uma boneca baphomet!";
				next;
				mes "[Elin]";
				mes "Aqui tem algo para você tambem.";
				getitem 525,5;
				mes "[Elin]";
				mes "Obrigada pela boneca!";
				emotion e_thx;
				close;
			}
			break;
		case 8:
			if(countitem(754) > 0){
				delitem 754,1;
				mes "[Elin]";
				mes "Ah, é uma boneca de fumacento.";
				mes "Eu odeio Fumacentos, mas a boneca é muito fofa! Obrigada!";
				emotion e_ok;
				next;
				mes "[Elin]";
				mes "Deixa eu te dar um pouco";
				mes "da torta feita em casa pela vovó!";
				getitem 539,3;
				next;
				mes "[Elin]";
				mes "Ela é muito boa então não deixe de come-la ok.";
				next;
				mes "[Elin]";
				mes "Obrigada novamente por essa boneca fofa!";
				emotion e_thx;
				close;
			}
			break;
		case 9:
			if(countitem(753) > 0){
				delitem 753,1;
				mes "[Elin]";
				mes "NOSSAA!!! Oh meu Deus!";
				mes "É uma boneca Yoyo!";
				mes "Isto é o que eu REALMENTE queria!!";
				mes "É tãoooo fofa......";
				emotion e_lv2;
				next;
				mes "[Elin]";
				mes "Como agradecimento, eu vou te dar isso.";
				getitem 608,1;
				next;
				mes "[Elin]";
				mes "Papai pegou elas";
				mes "durante as suas viagens.";
				mes "É uma semente de algum tipo.";
				mes "Nós tentamos planta-las em casa,";
				mes "mas elas não parecem crescer.";
				next;
				mes "[Elin]";
				mes "De qualquer modo, MUITOO obrigada";
				mes "pela boneca de yoyo!!!!";
				emotion e_thx;
				close;
			}
			break;
		case 10:
			mes "[Elin]";
			mes "BLAAAAAAAAHHH!!";
			mes "Não ria da minha cara só porque sou criança!";
			emotion e_pif;
			next;
			mes "[Elin]";
			mes "(murmura) .....'idiota'.....";
			close;
	}
	mes "[Elin]";
	mes "EI!! Você não tem";
	mes "nenhuma boneca...";
	mes "Eu não consigo acreditar";
	mes "que você mentiria para uma criancinha!!!";
	emotion e_gasp;
	next;
	mes "[Elin]";
	mes "Você é MALVADO!!";
	mes "EU TE ODEIO!!.....";
	mes "sniff... sniff.... Buuuuuuuuuuuuuuuaaaaaaaaaaa!!";
	emotion e_sob;
	close;
}

// Boy's Cap' Quest
alberta_in,28,145,4	script	Vovô	120,{
	mes "[Vovô]";
	mes "*Cof*...";
	mes "....*Cof!*";
	mes "Ah...";
	mes "Alguns devem dizer que vivi uma vida plena bebendo vinho, com mulheres e música...";
	next;
	mes "[Vovô]";
	mes "Mas minha alma ainda queima com um vigor jovem! É só... Que esse corpo decrépito não aguenta mais... *Aaatchim~*";
	next;
	if (countitem(1030) > 9)
	{
		switch(select("Mostrar a ele a Tiger's Footskin.:Trocar isso pelo Quepe.:Cancelar")) {
		case 1:
			mes "[Vovô]";
			mes "Ohhh~ !!";
			mes "Isso é...?";
			mes "É sim!! ";
			mes "Essa é a Tiger's Footskin!! Até daqui posso sentir seus poderes!";
			next;
			mes "[Vovô]";
			// Prazer vigoroso?
			//mes "One glance at it can restore your strength, one sniff and your blood boils with vigorous pleasure...";
			mes "Uma olhada nela pode revigorar sua força, uma cheirada e seu sangue ferve com um prazer vigoroso...";
			next;
			mes "[Vovô]";
			mes "Só uma mordida...";
			mes "Irá revivier minha virilidade!!!! A Tiger's Footskin~!! Ahhh! Meuu DEUS!! Por favor!!! Por favor me dê ela... Por favor...";
			close;
		case 2:
			delitem 1030,10;	//Tiger_Footskin
			getitem 5016,1;		//Boy's_Cap
			mes "[Vovô]";
			mes "O... Obrigado !!";
			mes "Com isso eu posso reviver meu esplendor jovem!! Devo come-las agora mesmo !";
			next;
			mes "[Vovô]";
			mes "...";
			mes "Nada. Ainda me sinto VELHO.";
			// ??
			//mes "If anything, I feel even worse. Th-there's this buzzing in my head...";
			mes "Se com qualquer coisa, eu me sinto ainda pior. T-Tem esse apito na minha cabeça...";
			close;
		case 3:
			mes "[Vovô]";
			mes "E...Ei, criança !! Esp-Espere !";
			close;
		}
	} 
	else {
		switch(select("Falar:Cancelar")) {
		case 1:
			mes "[Vovô]";
			mes "Escute bem...";
			mes "Você tem que tomar conta de sí mesmo tão bem quanto conseguir... Sem sua saúde, você não consegue aproveitar os prazeres desse mundo mortal.";
			next;
			mes "[Vovô]";
			mes "Para restaurar minha juventude, tentei todas as coisas possíveis, mais de rumores, mas nada funcionou. No final, gastei tantos zenys em curas milagrosas que, terminei quebrado.";
			next;
			mes "[Vovô]";
			mes "Quase desisti da minha causa, restaurar minha juventude. Mas tenho uma última esperança... Comer ^3355FFTiger's Footskin^000000, O Rei dos Revigorantes!!";
			next;
			mes "[Vovô]";
			mes "Nunca ouviu falar?! Um olhar poderá restaurar a cor de meus cabelos, uma cheirada, só um cheiro, e um oitentão teria outra chance de ser jovem... É O Rei dos Revigorantes!!";
			next;
			mes "[Vovô]";
			mes "Antes de morrer...";
			mes "Eu queria poder comer 10 Tiger's Footskin. Então, eu não teria que morrer!";
			next;
			mes "[Vovô]";
			mes "Se alguém me ajudasse a pega-las, eu daria a essa pessoa meu item precioso, ^3355FFQuepe^000000 sem qualquer remórcio...";
			close;
		case 2:
			mes "[Grampa]";
			mes "Cof Cof !!";
			mes "Tiger ....Tiger's ..... Foot ..... skin .....Cough Cough !!";
			close;
		}
	}
}

// Antlers Quest 
alberta_in,122,53,4	script	Cherokee	47,{
	mes "[Cherokee]";
	mes "OI,eu sou um colecionador de chifres...parece engraçado não?";
	next;
	mes "[Cherokee]";
	mes "Chifres de animais são muito valiosos e caros.";
	mes "Você pode vestil-os na cabeça ou coloca-los na parede em sua casa.";
	next;
	mes "[Cherokee]";
	mes "Tem um tipo de chifre que eu estive procurando a muito tempo,  É o ^5555FF'Chifre de Alce '^000000..";
	next;
	mes "[Cherokee]";
	mes "Alguns dizem que esse chifre é de um Demônio!";
	mes "Por isso que quero um desses.";
	next;
	mes "[Cherokee]";
	mes "diga, você tem alguns ^5555FF'Chifre Diabólico'^000000?";
	mes "se você me conceguir ^5533FF20 Chifres Diabólicos^000000, eu vou te dar minha preciosa ^FF3355'Chifre de Alce'^000000.  estamos de acordo?";
	next;
	if(select("Claro!:Eu heim. Chifres?.")==1){
		if(countitem(923) < 20){
			mes "[Cherokee]";
			mes "Hmm.... Eu disse que preciso de ^FF5533'20 Chifres Diabólicos'^000000.";
			close;
		}
		delitem 923,20;
		mes "Whoah~! é a primeira vez que eu vejo um ^3355FF'Chifres Diabólicos'^000000!!";
		emotion e_ic;
		next;
		mes "[Cherokee]";
		mes "Aqui está o Chifre de Alce!";
		emotion e_thx;
		getitem 2284,1;
		next;
		mes "[Cherokee]";
		mes "Muito obrigado por completar minha coleção!!!";
		close;
	}
	mes "[Cherokee]";
	mes "Nossa, espero que o Demônio crave os chifres dele bem no seu peito para você aprender a ser mais gentil....";
	close;
}


// Bao Bao, Cresent Hairpin, Fashionbale Glasses, Heart Hairpin
alberta,120,53,3	script	Zic	51,{
	mes "[Zic]";
	mes "Ahh! que vento refrescante!!";
	emotion e_ho;
	next;
	mes "[Zic]";
	mes "Huh?... você quer algo?";
	emotion e_hmm;
	next;
	mes "[Zic]";
	mes "~olhando~ minha reputação não está muito boa por isso venho aqui.....";
	next;
	mes "[Zic]";
	mes "Fale o que você quizer, talvez eu possa te ajudar.....";
	next;
	switch(select("- Bao-Bao:- Fivela de Lua:- Óculos Fashion:- Fivela de Coração")){
		case 1:
			mes "[Zic]";
			mes "Para fazer um Bao-Bao eu vou precisar dê:";	//5042
			mes "- ^5555FF1 Laço de Seda^000000,";	//10007
			mes "- ^5555FF50 Insígnias do Herói^000000.";	//968
			next;
			mes "[Zic]";
			mes "Quer que eu faça um?";
			next;
			if(select("Claro.:Não.")==1){
				if (countitem(10007)<1 || countitem(968)<50){
					mes "[Zic]";
					mes "Ei! Está faltando alguns itens, volte quando tiver tudo.";
					close;
				}
				delitem 10007, 1;
				delitem 968, 50;
				mes "[Zic]";
				mes "Espere, eu irei fazer o Bao-Bao......";
				next;
				mes "[Zic]";
				mes "Aqui está o Bao-Bao. Divirta-se.";
				getitem 5042, 1;
				close;
			}
			break;
		case 2:
			mes "[Zic]";
			mes "Para fazer uma Fivela de Lua vou precisar dê:";	//5048
			mes "- ^5555FF1 Fivela de Coração^000000,";	//5041
			mes "- ^5555FF10 Aços^000000.";	//999
			next;
			mes "[Zic]";
			mes "Quer fazer uma?";
			next;
			if(select("Claro.:Não.")==1){
				if (countitem(5041)<1 || countitem(999)<10){
					mes "[Zic]";
					mes "Ei! Está faltando alguns itens, volte quando tiver tudo.";
					close;
				}
				delitem 5041, 1;
				delitem 999, 10;
				mes "[Zic]";
				mes "Estou fazendo sua Fivela de Lua......";
				next;
				mes "[Zic]";
				mes "Aqui está!";
				getitem 5048, 1;
				close;
			}
			break;
		case 3:
			mes "[Zic]";
			mes "Para fazer um Óculos Fashion eu vou precisar dê:";	//5047
			mes "- ^5555FF1 Óculos Charmoso^000000,";	//2271
			mes "- ^5555FF1 Tintura Vermelha^000000.";	//975
			next;
			mes "[Zic]";
			mes "Quer um?";
			next;
			if(select("Claro.:Não.")==1){
				if (countitem(2271)<1 || countitem(975)<1){
					mes "[Zic]";
					mes "Ei! Está faltando alguns itens, volte quando tiver tudo.";
					close;
				}
				delitem 2271, 1;
				delitem 975, 1;
				mes "[Zic]";
				mes "Estou preparando ele......";
				next;
				mes "[Zic]";
				mes "Aqui está.";
				getitem 5047, 1;
				close;
			}
			break;
		case 4:
			mes "[Zic]";
			mes "Para fazer uma Fivela de Coração eu vou precisar dê:";	//5041
			mes "- ^5555FF1200 Corais^000000.";	//7013
			emotion e_hmm;
			next;
			mes "[Zic]";
			mes "Quer fazer um?";
			next;
			if(select("Claro.:Não.")==1){
				if (countitem(7013)<1200){
					mes "[Zic]";
					mes "Ei! Está faltando alguns itens, volte quando tiver tudo.";
					close;
				}
				delitem 7013, 1200;
				mes "[Zic]";
				mes "Espere um minuto......";
				next;
				mes "[Zic]";
				mes "Aqui está.";
				getitem 5041, 1;
				close;
			}
			break;
	}
	mes "[Zic]";
	mes "Até mais.";
	close;
}

// Hat of Sun God, Sunday Hat, Mage Hat, Magician Hat
alberta,135,79,3	script	Tempestra	71,{
	mes "[Tempestra]";
	mes "Ah...... Que vento gostoso.  É bom estar próximo ao mar.  Eu acho que foi uma ótima escolhe deixar meu trabalho e tomar um tempo aqui.";
	emotion 2;
	if(TEMPESTRA == 1) goto sM_Menu;
	next;
	mes "[Tempestra]";
	mes "O sol está tão brilhante hoje.  Eu ficarei feliz se trouxesse meu chapéu.  se minha pele focar exposta à um sol como o de hoje, Tenho certeza de que me queimarei.";
	next;
	mes "[Tempestra]";
	mes "Jovem, o calor está me fazendo mal.  Nada me faria melhor agora, como uma Poção Amarela bem cheia.....";
	next;
	menu "Então pegue-a, seu deleite.",M_0, "Vou conseguir uma, sheesh....",M_1;

	M_0:
		if(countitem(503) < 1) goto L_NoPot;
		set TEMPESTRA, 1;
		mes "[Tempestra]";
		mes "Oooh! Muito obrigada.  Estou feliz por encontrar uma pessoa tão simpática aqui....";
		emotion 15;
		next;
		mes "[Tempestra]";
		mes "(~gulp~gulp~)";
		next;
		mes "[Tempestra]";
		mes "Hyaaaaaaa!! É tão fria!! Obrigada.";
		sM_Menu:
		next;
		menu "Esta muito quente mesmo, não?...",sM_0, "Você deveria usar um chepéu.",sM_1;

		sM_0:
			mes "[Tempestra]";
			mes "Sim, está muito quente mesmo.....";
			next;
			mes "[Tempestra]";
			mes "Felizmente eu trouxe muitos chapéus. Eu tenho";
			mes "um ^5555FF'Chapéu de Verão'^000000,";
			mes "um ^5555FF'Chapéu de mestre Arcano'^000000,";
			mes "um ^5555FF'Cartola'^000000, e mais em meu cômodo...";
			next;
			mes "[Tempestra]";
			mes "Eu trouxe esses chapéus comigo então eu posso vendê-los para aumentar minhas férias....";
			mes "Mas agora parece que tenho de usar um.....";
			ssM_Menu:
			next;
			menu "Chapéu de Verão?",ssM_0a, "Chapéu de mestre Arcano?",ssM_0b, "Cartola?",ssM_0c, "Terminar a Conversa.",ssM_End;

			ssM_0a:
				mes "[Tempestra]";
				mes "Um Chapéu de Verão é feito de:";
				mes "- ^5555FF250 Tecido^000000,";
				mes "- ^5555FF1 Chapéu^000000,";
				mes "- ^5555FF1 Barrete^000000,";
				mes "- ^5555FF600 Penas Macias^000000.";
				mes "Se quiser, posso fazer um para você.  Somente me traga os itens.";
				if (countitem(1059)<250 || countitem(2221)<1 || countitem(2227)<1 || countitem(7063)<600) goto ssM_Menu;
				next;
				menu "Faça um para mim.",-, "Cancelar",ssM_End;

					mes "[Tempestra]";
					mes "Por favor aguarde um momento.....";
					next;
					getitem 5032, 1;
					delitem 1059, 250;
					delitem 2221, 1;
					delitem 2227, 1;
					delitem 7063, 600;
					mes "[Tempestra]";
					mes "Tudo pronto, um Chapéu de Verão para você. Obrigado e volte quando quiser.";
					emotion 15;
					close;
			ssM_0b:
				mes "[Tempestra]";
				mes "Um Chapéu de mestre Arcano é feito de:";
				mes "- ^5555FF1 Chapéu de Bathory^000000,";
				mes "- ^5555FF400 Escamas de Dragão^000000,";
				mes "- ^5555FF50 Pó de Bolor^000000,";
				mes "- ^5555FF1 Carta Salgueiro Ancião^000000.";
				mes "Se quiser, posso fazer um para você.  Somente me traga os itens.";
				if (countitem(2252)<1 || countitem(1036)<400 || countitem(7001)<50 || countitem(4052)<1) goto ssM_Menu;
				next;
				menu "Faça um para mim.",-, "Cancelar",ssM_End;

					mes "[Tempestra]";
					mes "Por favor aguarde um momento.....";
					next;
					getitem 5027, 1;
					delitem 2252, 1;
					delitem 1036, 400;
					delitem 7001, 50;
					delitem 4052, 1;
					mes "[Tempestra]";
					mes "Tudo pronto, um Chepéu de mestre Arcano para você. Obrigado e volte quando quiser.";
					emotion 15;
					close;
			ssM_0c:
				mes "[Tempestra]";
				mes "Uma Cartola é feita de:";
				mes "- ^5555FF1 Chapéu de Bathory^000000,";
				mes "- ^5555FF450 Lábios Focilizados^000000,";
				mes "- ^5555FF1200 Casca Rija^000000,";
				mes "Se quiser, posso fazer um para você.  Somente me traga os itens.";
				if (countitem(2252)<1 || countitem(1054)<450 || countitem(943)<1200) goto ssM_Menu;
				next;
				menu "Faça um para mim.",-, "Cancelar",ssM_End;

					mes "[Tempestra]";
					mes "Por favor aguarde um momento.....";
					next;
					getitem 5045, 1;
					delitem 2252, 1;
					delitem 1054, 450;
					delitem 943, 1200;
					mes "[Tempestra]";
					mes "Tudo pronto, uma Cartola para você. Obrigado e volte quando quiser.";
					emotion 15;
					close;
			ssM_End:
				close;

		sM_1:
			mes "[Tempestra]";
			mes "Oh mas meus chapéus não estão a venda.... se eu usar um não poderei vender aos meus fregueses.";
			next;
			mes "[Tempestra]";
			mes "Por causa de você ter me dado a poção, eu vou fazer algo pra você. Mas infelizmente não tenho os materiais agora";
			next;
			mes "[Tempestra]";
			mes "Hmm... Se você tiver:";
			mes "^5555FF1 Símbolo do Deus-Sol";
			mes "10 Ouro";
			mes "40 Aço";
			mes "50 Carvão";
			mes "e 2 Oridecon^000000..........";
			next;
			mes "[Tempestra]";
			mes "Eu posso fazer um ^5555FF'Coroa do Deus Sol'^000000!! É uma obra fantástica. Como isso soa?";
			next;
			menu "Claro.",ssM_1a, "Não, agora não, obrigado.",ssM_1b;

			ssM_1a:
				if(countitem(7086)<1 || countitem(969)<10 || countitem(999)<40 || countitem(1003)<50 || countitem(984)<2) goto ssL_NotEnuf;
				mes "[Tempestra]";
				mes "Por favor aguarde um momento.....";
				next;
				getitem 5022, 1;
				delitem 7086, 1;
				delitem 969, 10;
				delitem 999, 40;
				delitem 1003, 50;
				delitem 984, 2;
				mes "[Tempestra]";
				mes "Tudo pronto... uma Coroa do Deus-Sol para você. Obrigada pela poção, e por favor volte a qualquer hora.";
				emotion 15;
				close;

				ssL_NotEnuf:
					mes "[Tempestra]";
					mes "Um.... O que são esses itens exatamente???";
					emotion 20;
					close;
			ssM_1b:
				mes "[Tempestra]";
				mes "Volte quando mudar de idéia.";
				close;

		L_NoPot:
			mes "[Tempestra]";
			mes "Umm... desculpe-me mas.... onde está a poção??";
			emotion 20;
			close;

	M_1:
		mes "[Tempestra]";
		mes "..... Que rude!!";
		emotion 0;
		close;
}
