//===== Cronus Script ======================================== 
//= Script ToHigh e BlockHigh
//===== Por: =================================================
//= Lupus
//===== Versão Atual: ======================================== 
//= 1.5
//============================================================

function	script	F_ToHigh	{
	if(Upper == 2) return; //Baby Class - skip it
	set @fjob,ADVJOB; //alternative classes should pass, too
	if(@fjob == Job_Rogue + 4001 ) set @fjob,Job_Assassin+4001;
	if(@fjob == Job_Alchem + 4001 ) set @fjob,Job_Blacksmith+4001;
	if(@fjob == Job_Bard + 4001 || @fjob == Job_Dancer + 4001 ) set @fjob,Job_Hunter+4001;
	if(@fjob == Job_Crusader + 4001 ) set @fjob,Job_Knight+4001;
	if(@fjob == Job_Monk + 4001 ) set @fjob,Job_Priest+4001;
	if(@fjob == Job_Sage + 4001 ) set @fjob,Job_Wizard+4001;

	if(Class == 3977+getarg(0) ) goto L_WELCOME; //3rd Job
	if(Class >= 4008) goto L_ALREADY; //already advanced class
	if(Class == 4001 && @fjob == 3977+getarg(2) ) goto L_GETHIGH; //High Novice -> High XXXX
	if(Class == 4001) show "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher.";
	if(Class == 4001) close;
	return; //this char doesn't want to get HIGH class

L_GETHIGH:
	show "["+getarg(8)+"]";
	if(JobLevel < 10) goto L_NOTREADY;
	if(checkcart(0)) goto L_CART;
	if(checkfalcon(0)) goto L_FALCON;
	if(checkriding(0)) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	show "Hello there, "+strcharinfo(0)+"!";
	show "You've made a brave choice in coming here to be reborn and stepping forth into the advanced ranks.";
	show "Now... close your eyes.";
	next;
	show "["+getarg(8)+"]";
	show "Open your eyes.";
	show "You have become a "+getarg(1)+".";
	setjob getarg(0); //High Class
	next;
	if (!(QSK1 || QSK2 || QSK3 || QSK4)) goto L_NO_QSKILL;
	show "["+getarg(8)+"]";
	show "Let me just add in the missing Quest Skills you lost under the Reborn process, "+strcharinfo(0)+".";
	next;
//return learnt quest skills
	if (getarg(4)) skill getarg(4),QSK1,0;
	set SQK1,0;
	if (getarg(5)) skill getarg(5),QSK2,0;
	set SQK2,0;
	if (getarg(6)) skill getarg(6),QSK3,0;
	set SQK3,0;
	if (getarg(7)) skill getarg(7),QSK4,0;
	set SQK4,0;

L_NO_QSKILL:
	show "["+getarg(8)+"]";
	show "I wish you good fortune in the near future!";
	emotion 46;
	close;

L_NOTREADY:
	show "You are not ready to become a "+getarg(1)+".";
	show "You have to raise your Job Level to 10.";
	emotion 0;
	close;

L_SKILLPNTS:
	show "You will need to use up all of your skill points if you want me to continue.";
	emotion 20;
	close;

L_CART:
	show "Please, drop your cart and we'll continue.";
	emotion 20;
	close;

L_FALCON:
	show "Please, free your Falcon and we'll continue.";
	emotion 20;
	close;

L_PECO:
	show "Please, free your Pecopeco and we'll continue.";
	emotion 20;
	close;

L_WELCOME:
	show "["+getarg(8)+"]";
	show "You are welcome, "+strcharinfo(0)+"!";
	show "We're always glad to see a "+getarg(1)+" here!";
	close;

L_ALREADY:
	show "["+getarg(8)+"]";
	show "It's such a big honor to salute envoys of Valhalla.";
	show "Come again.";
	emotion 2;
	close;
}

// function GET 3rd JOB
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
function	script	F_Rebirth	{
	show "["+getarg(3)+"]";
	if(Upper == 2) goto L_BABY; //Baby Class - skip it
	if(Class >= 4008) goto L_ALREADY; //already advanced class
	if(Class == (3977+getarg(0)) && ADVJOB == (3977+getarg(2))) goto L_GET3RD; //Hight XXXX -> 3rd Job
	show "Go talk to either Valhallana or one of my collegues...";
	emotion 17;
	close;

L_GET3RD:
	if(JobLevel < 45) goto L_NOTREADY;
	if(checkcart(0)) goto L_CART;
	if(checkfalcon(0)) goto L_FALCON;
	if(checkriding(0)) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	show "Congratulations!";
	show "You have trained well. Now stroll here as a "+getarg(3)+"!";
	setjob getarg(2); //Rebirth Class
	set ADVJOB,0;
	close;

L_NOTREADY:
	show "You have went so far to get here. But I am sorry, you aren't quite ready to become a "+getarg(3)+".";
	show "You need at least Job Level 45 or higher.";
	emotion 0;
	close;

L_SKILLPNTS:
	show "You will need to use up all of your skill points if you want me to continue.";
	emotion 20;
	close;

L_CART:
	show "Please, drop your cart and we'll continue.";
	emotion 20;
	close;

L_FALCON:
	show "Please, free your Falcon and we'll continue.";
	emotion 20;
	close;

L_PECO:
	show "Please, free your Pecopeco and we'll continue.";
	emotion 20;
	close;

L_ALREADY:
	show "Well, hello there! You have been reborn once, there is no second chance.";
	emotion 2;
	close;

L_BABY:
	show "What a lively baby!";
	show "How did you get here? Go to aunt Valhallana and ask her to take your home.";
	emotion 41;
	close;

}

// function GUIDE player to Valhalla for getting 3rd JOB
// this function prevents passing 2nd JOB QUESTS by advanced classes also
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
//getarg(4) - NPC Name
function	script	F_BlockHigh	{
	if(Upper != 1) return; //This func should interact with Advanced classes only
	show "["+getarg(4)+"]";
	if(Class == (3977+getarg(0)) && ADVJOB == (3977+getarg(2))) goto L_RIGHTHIGH;
	if(Class == (3977+getarg(2))) goto L_RIGHT3RD;
	if(Class >= 4008) goto L_ALREADY3RD; //already advanced class, but from wrong guild
//this player is a High Novice
	if(Class == 4001) show "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher.";
//this player has 1st advanced job, but from wrong guild
	if(Class == (3977+getarg(0))) show "A "+getarg(1)+"?";
	if(Class != 4001) show "Rumors say only Valhallana knows your way...";
	emotion 17;
	close;

L_RIGHT3RD:
	show "Well, hello there! You look... younger.";
	emotion 20;
	next;
	show "["+getarg(4)+"]";
	show "You are always welcome here, "+strcharinfo(0)+"!";
	show "Our good old guild is your second home, isn't it?";
	emotion 21;
	close;

L_RIGHTHIGH:
	show "Hello, "+strcharinfo(0)+"!";
	show "If you are going to become a "+getarg(3)+", then you should visit Valhalla again.";
	if(JobLevel < 45) show "But you need at least Job Level 45 or higher.";
	emotion 0;
	close;

L_ALREADY3RD:
	show "A "+getarg(3)+"?";
	show "You don't belong to our guild. Begone!";
	emotion 23;
	close;

}