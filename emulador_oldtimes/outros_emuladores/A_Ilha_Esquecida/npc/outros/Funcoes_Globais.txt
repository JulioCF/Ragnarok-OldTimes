//===== Cronus Script ======================================= 
//= Global Functions
//===== Por: ================================================== 
//= Lupus, kobra_k88
//===== Versão: ===================================== 
//= 1.4/2.08
//============================================================

//=========================================
// Function that clears job quest variables
//=========================================

function	script	F_ClearJobVar	{
	// Treinamento -------------------------
	set Tr_naoaceitoclass,0; //Apagar variavel do treinamento (Não Remova)
	// Misc ---------------------------------
	set JBLVL,0;
	set FIRSTAID,0;
	set PLAYDEAD,0;
	set got_bandage,0;
	set got_novnametag,0;
	// First Class Jobs ---------------------
	set job_acolyte_q,0; set job_acolyte_q2,0;
	set job_archer_q,0;
	set job_magician_q,0;
	set job_merchant_q,0; set job_merchant_q2,0; set job_merchant_q3,0;
	set job_sword_q,0; set SWTEST, 0;
	set job_thief_q,0;
	// Super Novice
	set SUPNOV_Q,0;
	// 2-1 Jobs ------------------------------
	set ASSIN_Q,0; set ASSIN_Q2,0;
	set BSMITH_Q,0; set BSMITH_Q2,0;
	set HNTR_Q,0; set HNTR_Q2,0;
	set KNIGHT_Q,0; set KNIGHT_Q2,0;
	set PRIEST_Q,0; set PRIEST_Q2,0; set PRIEST_Q3,0;
	set WIZ_Q,0; set WIZ_Q2,0;
	// 2-2 Jobs ------------------------------
	set ROGUE_Q,0; set ROGUE_Q2,0;
	set ALCH_Q,0; set ALCH_Q2,0;
	set CRUS_Q,0;
	set MONK_Q,0; set JOB_MONK_C,0;
	set SAGE_Q,0; set SAGE_Q2,0;
	set DANC_Q,0;
	set BARD_Q,0;
	// Extended Classes
	set TAEK_Q,0; set TK_Q,0;
	set STGL_Q,0;
	set SOUL_Q,0;
	set GUNS_Q,0;
	set NINJ_Q,0;
	return;
}

function	script	F_ClearGarbage	{
	// Clear outdated global VARS
	//DTS Vote System
	if($dtsglobalelig) set $dtsglobalelig,0;
	if($dts&(1<<6)) set $dts,$dts&~(1<<6);
	//Cube Room
	if(hzdun) {
		set MISC_QUEST,MISC_QUEST|512;
		set num1,0;	set num2,0;	set num3,0;	set num4,0;	set num5,0;
		set num6,0;	set num7,0;	set num8,0;	set num9,0;
		set hzdun,0; set cubekey,0; set piciburn,0;
	}
	//Airship
	if($lengths){
		cleararray $textstringsa$[0],"",getarraysize($textstringsa$);
		cleararray $textstringsb$[0],"",getarraysize($textstringsb$);
		cleararray $textstringsc$[0],"",getarraysize($textstringsc$);
		cleararray $compstring$[0],"",getarraysize($compstring$);
		cleararray $lengths[0],0,getarraysize($lengths);
	}
	//Shinkskas Quest garbage
	if(Shinokas_Quest > 7){
		set ein_scientist,0;
		set ein_stuff,0;
	}
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// Used in REBIRTH scripts
// Class = Internal Class ID
// BaseJob = Base Job (0..23)
// Upper : 0 - Default, 1 - Advanced, 2 - Baby
//////////////////////////////////////////////////////////////////////////////////

function	script	Job_Change	{
	if (Upper<2) 
		setjob getarg(0); // Common jobchange
	else 
		setjob getarg(0) + Job_Baby; //Change Job For Baby Class
	logmes "CLASS CHANGE: " +strcharinfo(0)+ " become a "+jobname(Class);
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// FOT BACKWISE COMPATIBILITY: Functions used to check a players job class
// HOW TO USE:
// i.e. We need all holy classes but monks
// if ( callfunc("Is_Holy_Class") && callfunc("Is_Monk")==0 ) goto L_Start;
//////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Aco,Monk,Priest,Aco High,High Priest,
// Champion, 0 otherwise
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Holy_Class	{
	return ( BaseJob==Job_Acolyte || BaseJob==Job_Priest || BaseJob==Job_Monk );
}


//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Archer,Hunter,Bard,Dancer,Archer High,Sniper,
// Clown,Gypsy, 0 otherwise
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Bow_Class	{
	return ( BaseJob==Job_Archer || BaseJob==Job_Hunter || BaseJob==Job_Bard || BaseJob==Job_Dancer );
}


//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Mage,Wizard,Sage,Mage High,High Wizard,
// Professor, 0 otherwise
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Magic_Class	{
	return ( BaseJob==Job_Mage || BaseJob==Job_Wizard || BaseJob==Job_Sage );
}


//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Merc,Blacksmith,Alchemist,Merc High,
// Whitesmith,Creator, 0 otherwise
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Merc_Class	{
	return ( BaseJob==Job_Merchant || BaseJob==Job_Blacksmith || BaseJob==Job_Alchem );
}


//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Thief,Assassin,Rogue,Thief High, Assassin Cross
// Stalker, 0 otherwise
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Thief_Class	{
	return ( BaseJob==Job_Thief || BaseJob==Job_Assassin || BaseJob==Job_Rogue );
}


//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Swordy,Knight,Crusader,Swordy High,
// Lord Knight,Paladin, 0 otherwise
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Sword_Class	{
	return ( BaseJob==Job_Swordman || BaseJob==Job_Knight || BaseJob==Job_Knight2 || BaseJob==Job_Crusader || BaseJob==Job_Crusader2 );
}


//////////////////////////////////////////////////////////////////////////////////
// returns 1 if the player is either Taekwon, Star Gladiator,Soul Linker, 0 otherwise
// these classes can't be adopted nor reborn ()
//////////////////////////////////////////////////////////////////////////////////

function	script	Is_Taekwon_Class	{
	return ( Class==Job_Taekwon || Class==Job_Star_Gladiator || Class==Job_Star_Gladiator2 || Class==Job_Soul_Linker );
}

//////////////////////////////////////////////////////////////////////////////////
// Functions used to spiff up dialoges [Lupus]
//////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_RandMes"
//////////////////////////////////////////////////////////////////////////////////
//returns random string
// Example: check F_Bye or F_Hi functions
// READ AND THINK: You can use it to pick a random number form list:
// set @itemIDfromList, callfunc("F_RandMes",8,1129,1222,1163,1357,1360,1522,1811,1410);

function	script	F_RandMes	{
	return getarg(rand(1,getarg(0)));
}

//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_Sex"
//////////////////////////////////////////////////////////////////////////////////
//returns 1st string if female, 2nd string otherwise
// Example: mes callfunc("F_Sex","What a beautiful lady!","What a handsom man!");

function	script	F_SexMes	{
	return getarg(Sex);
}

//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_Hi"
//////////////////////////////////////////////////////////////////////////////////
//returns random HELLO message

function	script	F_Hi	{
	return callfunc("F_RandMes",5,"Hi!","Hello!","Good day!","How are you?","Hello there.");
}


//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_Bye"
//////////////////////////////////////////////////////////////////////////////////
//returns random BYE message

function	script	F_Bye	{
	return callfunc("F_RandMes",6,"Bye. See you again.","Later.","Goodbye.","Good luck!","Have a nice day!","Byebye!!!");
}

//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_ItemName"
//////////////////////////////////////////////////////////////////////////////////
// Returns expanded item name string
//Argumentss
// 0 - Item ID
// 1 - Element N (0=none,1=Ice,2=Earth,3=Fire,4=Wind)
// 2 - VVS meter 0..3
// 3 - Refine
// Example: mes "Show me your "+callfunc("F_ItemName",1201,1,2,5)+"...";
//  is equal to: mes "Show me your ^000090 +5 VVS Fire Knife ^000000..."

function	script	F_ItemName	{
	set @t$,"^000090";
	if(getarg(3)) set @t$,@t$+"+"+getarg(3)+" ";
	if(getarg(2)==1) set @t$,@t$+"VS ";
	if(getarg(2)==2) set @t$,@t$+"VVS ";
	if(getarg(2)==3) set @t$,@t$+"VVVS ";
	if(getarg(2)>3) set @t$,@t$+getarg(2)+"xVS ";
	if(getarg(1)==1) set @t$,@t$+"Ice ";
	if(getarg(1)==2) set @t$,@t$+"Earth ";
	if(getarg(1)==3) set @t$,@t$+"Fire ";
	if(getarg(1)==4) set @t$,@t$+"Wind ";
	if(getarg(1)>4) set @t$,@t$+"Strange ";
	return @t$+getitemname(getarg(0))+"^000000";
}


//////////////////////////////////////////////////////////////////////////////////
// *** Function "getJobName"							//
// [Usage] : callfunc("getJobName",Class);                                      //
//////////////////////////////////////////////////////////////////////////////////

function	script	getJobName	{
	return jobname(getarg(0));
}
//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_SaveQuestSkills": Store learnt quest skills 
//////////////////////////////////////////////////////////////////////////////////
function	script	F_SaveQuestSkills	{
	set ADV_QSK,0; set ADV_QSK2,0;
	//1st classes quest skills
	for(set @i, 0; @i < 14; set @i, @i + 1){
		if(getskilllv(144+@i)) set ADV_QSK,ADV_QSK|pow(2,@i);
	}
	//2nd classes quest skills
	for(set @i, 0; @i < 19; set @i, @i + 1){
		if(getskilllv(1001+@i)) set ADV_QSK2,ADV_QSK2|pow(2,@i);
	}
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_Load1Skills": Restore learnt 1st class quest skills
//////////////////////////////////////////////////////////////////////////////////
function	script	F_Load1Skills	{
	//1st classes quest skills
	for(set @i, 0; @i < 14; set @i, @i + 1){
		if(ADV_QSK|pow(2,@i) == ADV_QSK) skill 144+@i,1,0;
	}
	set ADV_QSK,0; //Clear var
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// *** Function "F_Load2Skills": Restore learnt 2nd class quest skills
//////////////////////////////////////////////////////////////////////////////////
function	script	F_Load2Skills	{
	//2nd classes quest skills
	for(set @i, 0; @i < 19; set @i, @i + 1){
		if(ADV_QSK2|pow(2,@i) == ADV_QSK2) skill 1001+@i,1,0;
	}
	set ADV_QSK2,0; //Clear var
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// Funções by [Mehah]
//////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////
// *** "QuestSkill"
//////////////////////////////////////////////////////////////////////////////////
function	script	questskill	{
	setarray @questskill[1],144,145,146,147,148,149,150,151,152,153,154,155,156,157;
	return;
}

function	script	skill:questsave	{
	callfunc("questskill");
	for(set @i,1;@i<=getarraysize(@questskill);set @i,@i+1)
		if(getskilllv(@questskill[@i])) setarray @SaveSkill[@i],@questskill[@i];
	return;
}

function	script	skill:questload	{
	for(set @i,1;@i<=getarraysize(@SaveSkill);set @i,@i+1)
		if(@SaveSkill[@i]) skill @SaveSkill[@i],1,0;
	return;
}	

function	script	skill:add	{
	if (Zeny < getarg(0)){
		show "Desculpe, mas você não possui zeny suficiente.";
		close;
	}
	for(set @i,1; @i <= getarg(1); set @i,@i+1){
		if(getskilllv(getarg(@i+1)) == FALSE){
			skill getarg(@i+1),1,0;
			setarray @skill_name$[@i],callfunc("getskillname",getarg(@i+1));
		}else
		if(@i == getarg(1)){
			show "Você já possui as habilidades!";
			close;
		}
	}
		set zeny, Zeny - getarg(0);
		show "Parabéns, acabou de receber sua(s) skill(s).";
		for(set @i,1;@i <= getarraysize(@skill_name$); set @i,@i+1)
			show @skill_name$[@i];
		cleararray @skill_name$[1],"",getarraysize(@skill_name$);
		close;
}
////////////////////////////////////////////////////////////////////////////////// 
// *** "GetSkillName": Retorna o nome da Habilidade identificada pelo id 
////////////////////////////////////////////////////////////////////////////////// 
function	script	getskillname	{ 

	switch(getarg(0)){ 
		case 142: return "Primeiros Socorros"; 
		case 143: return "Fingir de Morto"; 
		case 144: return "Recuperar HP em Movimento"; 
		case 145: return "Ataque Fatal"; 
		case 146: return "Instinto de Sobrevivência"; 
		case 147: return "Fabricar Flechas"; 
		case 148: return "Disparo Violento"; 
		case 149: return "Chutar Areia"; 
		case 150: return "Recuar"; 
		case 151: return "Procurar Pedras"; 
		case 152: return "Arremessar Pedra"; 
		case 153: return "Cavalo-de-Pau"; 
		case 154: return "Personalizar Carrinho"; 
		case 155: return "Grito de Guerra"; 
		case 156: return "Luz Divina"; 
		case 157: return "Proteção Arcana"; 
		case 1001: return "Avanço Ofensivo"; 
		case 1002: return "Submissão"; 
		case 1003: return "Lâminas Aceleradas"; 
		case 1004: return "Faca Envenenada"; 
		case 1005: return "Confinamento"; 
		case 1006: return "Explosão Protetora"; 
		case 1007: return "Criar Conversor Elemental"; 
		case 1008: return "Mudança Elemental - Água"; 
		case 1009: return "Flecha Fantasma"; 
		case 1010: return "Voz Dolorosa"; 
		case 1011: return "Piscadela"; 
		case 1012: return "Venda Duvidosa"; 
		case 1013: return "Ganância"; 
		case 1014: return "Martírio"; 
		case 1015: return "Concessão Espiritual"; 
		case 1016: return "Punhos Intensos"; 
		case 1017: return "Mudança Elemental - Terra"; 
		case 1018: return "Mudança Elemental - Fogo"; 
		case 1019: return "Mudança Elemental - Vento"; 
		case 8001: return "Cura pelas Mãos"; 
		case 10000: return "Autorização Oficial"; 
		case 10001: return "Contrato com Kafra"; 
		case 10002: return "Comandar Guardiões"; 
		case 10003: return "Fortalecer Guardiões"; 
		case 10004: return "Expandir Clã"; 
		case 10005: return "Glória da Guilda"; 
		case 10006: return "Grande Liderança"; 
		case 10007: return "Ferimentos de Glória"; 
		case 10008: return "Coração Frio"; 
		case 10009: return "Olhar Apurado"; 
		case 10010: return "Comando de Batalha"; 
		case 10011: return "Regeneração"; 
		case 10012: return "Restauração"; 
		case 10013: return "Chamado Urgente"; 
		case 10014: return "Desenvolvimento Permanente"; 
		default: return "^FF0000Habilidade Desconhecida^000000"; 
	} 
}