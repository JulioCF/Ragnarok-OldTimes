//===== Cronus Script ========================================
//= Tony
//===== Por: =================================================
//= Playtester, Paradox924X
//===== Versão Atual: ========================================
//= 1.1a/1.3
//===== Descrição: ===========================================
//= Negocia Projéteis
//===== Comentários Adicionais: ==============================
//= 1.0 Traduzido [Kamper]
//= 1.1 Adicionado nome de itens como argumento [Kamper]
//= 1.1a Erros de português corrigido por @Coja ImortalRO Staff
//============================================================

que_ng,187,156,3	script	Negociador de Projéteis	86,{

	show "[Tony]";
	if (BaseJob == Job_Gunslinger) {
		show "Ei, meu nome é Tony.";
		show "Eu sou responsável pela distribuição e fabricação";
		show "de projéteis para Justiceiros.";
		next;
		show "[Tony]";
		show "Bem, grande rapaz!";
		show "Do que você precisa?!";
		show "Escolha o que desejar!";
		show "E pague!";
		next;
		switch(select("Esfera Venenosa:Esfera de Chamas:Esfera de Relâmpagos:Esfera Cegante:Esfera Congelante:Cancelar")) {
		case 1:	callfunc "Bullet_Trade",937,10,13205,"Esferas Venenosas","Caninos Venenosos"; break;
		case 2:	callfunc "Bullet_Trade",7097,2,13203,"Esferas de Chamas","Corações Incandescentes"; break;
		case 3:	callfunc "Bullet_Trade",7053,3,13204,"Esferas de Relâmpagos","Cyfars"; break;
		case 4:	callfunc "Bullet_Trade",1024,5,13206,"Esferas Cegantes","Tintas de Polvo"; break;
		case 5:	callfunc "Bullet_Trade",7054,2,13207,"Esferas Congelantes","Brigans"; break;
		default:
			show "[Tony]";
			show "Hmm. Okay.";
			show "Volte quando desejar!";
			show "Eu, Tony, o Negociador de Projéteis!";
			show "Sempre estarei aqui!!!";
			close;
		}
	}
	show "[Tony]";
	show "Ei, meu nome é Tony.";
	show "Eu sou responsável pela distribuição e fabricação";
	show "de projéteis para Justiceiros.";
	next;
	show "[Tony]";
	show "Lamento se você veio";
	show "até aqui para comprar munição.";
	show "Eu só posso negociar com Justiceiros credênciados.";
	close;
}

function	script	Bullet_Trade	{
	show "[Tony]";
	show "Digite a quantidade que deseja.";
	next;
	show "[Tony]";
	show "Eu negocio 30 "+getarg(3)+" por";
	show "1 Fracon,";
	show "1 Emveretarcon,";
	show "e "+getarg(1)+" "+getarg(4)+".";
	next;
	show "[Tony]";
	show "O número máximo que você pode negociar é 500.";
	show "Se você quiser cancelar, digite 0.";
	next;
	digit .@amount;
	show "[Tony]";
	if (.@amount < 1 || .@amount > 500) {
		show "Quantidade inválida!";
		show "Tente novamente!";
		close;
	} else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {		
			show "Nossa... Seu inventário está realmente cheio! Volte quando ele estiver mais espaço.";
			close;
		} else {
			show "Oh, bom!";
			show "Um ótimo negócio...";
			show "Ai está! O que queria.";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			additem getarg(2),.@amount * 30;
			close;
		}
	} else {
		show "Tsc Tsc~";
		show "Você não possuí";
		show "os itens";
		show "que precisava.";
		show "Prepara-os e volte.";
		close;
	}
