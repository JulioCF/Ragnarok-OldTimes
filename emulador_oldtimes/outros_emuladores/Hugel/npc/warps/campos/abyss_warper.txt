//===== Athena Script ========================================
//= Abyss Cave Warper
//===== By: ==================================================
//= erKURITA
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Warper to Abyss Cave. and warper out
//===== Additional Comments: =================================
//= 1.0 Added by Nexon [Nexon]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Fixed tab - missing pillar's appeared [Lupus]
//= 1.3 Updated Abyss Lake entrance based on official [SinSloth]
//= 1.3a Removed .GATs [Lupus]
//============================================================
// Traduzido por Ice~* 
//============================================================

hu_fild05,168,304,0	script	Coluna	111,{

	show "^3355FFHá 3 encaixes";
	show "estranhamente feitos";
	show "na superfície desta";
	show "coluna. Parece que";
	show "determinadas peças do corpo do dragão";
	show "caberiam perfeitamente nos";
	show "encaixes dessa coluna.^000000";
	if((countitem(1035)) && (countitem(1036)) && (countitem(1037)))
	{
		next;
		switch( select( "Encaixar partes do Dragão","Comer partes do Dragão" ) )
		{
			case 1:
				show "^3355FFVocê cuidadosamente coloca o";
				show "Canino de Dragão em um dos encaixes";
				show "e então você ouve";
				show "um estrondo poderoso";
				show "vindo da coluna.^000000";
				specialeffect 52;
				next;
				show "^3355FFVocê vagarosamente coloca";
				show "a Escama de Dragão em outro encaixe";
				show "da coluna e sente";
				show "outro pequeno tremor vindo";
				show "de dentro da coluna.^000000";
				specialeffect 52;
				next;
				show "^3355FFVocê introduz cautelosamente";
				show "a Cauda de Dragão em um";
				show "último encaixe. Luzes brilham";
				show "dentre as pequenas rachaduras";
				show "na superfície da coluna...^000000";
				specialeffect 52;
				next;
				show "^3355FFA terra abaixo de";
				show "seus pés, começa a";
				show "agitar violentamente.^000000";
				specialeffect 73;
				donpcevent "AbyssWarp::OnWarp";
				specialeffect2 36;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				close2;
				warp "hu_fild05",184,204;
				end;

			case 2:
				show "^3355FFVocê coloca o";
				show "Canino de Dragão, a Escama";
				show "de Dragão, e a Cauda de Dragão dentro";
				show "da sua boca e começa a mastigar";
				show "devagar. Nada acontece";
				show "e o gosto desses artigos";
				show "começa a ficar podre.^000000";
				next;
				emotion e_rice,1;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				percentheal -10,0;
				close;
		}
	}
	close;
}

hu_fild05,171,211,0	script	Coluna	111,1,1,{

	show "^3355FFEsta coluna é";
	show "muito similar a que";
	show "você viu no lago.";
	show "Há um exímio";
	show "encaixe azul";
	show "na superfície.^000000";
	next;
	switch( select( "Tocar o encaixe","Remover item do encaixe" ) )
	{
		case 1:
			show "^3355FF*Clatter Clatter*^000000";
			next;
			show "^3355FFA luz emitida";
			show "pelo encaixe se distorce e";
			show "a coluna começa a se agitar.";
			show "Você ouve um pequeno estalo";
			show "vindo de dentro da coluna.^000000";
			specialeffect 52;
			next;
			show "*Ggghhhhhzzzz!*";
			show "*BAM!*";
			next;
			show "^3355FFO raio de luz vindo";
			show "do encaixe da coluna começa";
			show "a brilhar e você sente";
			show "a terra abaixo dos seus pés";
			show "começar a afundar lentamente...^000000";
			next;
			specialeffect 73;
			specialeffect2 36;
			close2;
			warp "hu_fild05",169,305;
			end;

		case 2:
			show "^3355FF*Ppppsssh!*";
			show "Você acidentalmente";
			show "quebra o item.^000000";
			specialeffect 52;
			next;
			show "^3355FF*Gggggggghhhhhhhhzzzzzzjjjjhh!*^000000";
			next;
			show "^3355FFA luz emitida pelo";
			show "encaixe da coluna se distorce e";
			show "a coluna começa a tremer.";
			show "Os tremores no chão";
			show "dificultam ficar de pé";
			show "firmemente, mas de repente";
			show "você é teleportado para algum lugar...^000000";
			next;
			specialeffect 73;
			specialeffect2 36;
			close2;
			warp "hu_fild05",157,284;
			end;
	}
}

hu_fild05,196,210,1	script	AbyssWarp	45,2,2,{

OnInit:
	disablenpc "AbyssWarp";
	end;

OnTouch:
	warp "abyss_01",260,268;
	end;

OnWarp:
	initnpctimer;
	enablenpc "AbyssWarp";
	end;

OnTimer30000:
	stopnpctimer;
	disablenpc "AbyssWarp";
	end;
}
