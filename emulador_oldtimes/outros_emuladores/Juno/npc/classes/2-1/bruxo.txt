//===== Cronus Script ======================================== 
//= Script Bruxo
//===== Por: =================================================
//= eAthena Dev Team
//===== Versão Atual: ======================================== 
//= 2.2
//===== Descrição: =========================================== 
//= Muda a classe para Bruxo
//===== Changelog: =========================================== 
//= Traduzido [Viv]
//= Organizado [Kamper]
//= Traduzido o que faltava [Kamper]
//= Quest Atualizada [Mehah]
//= Adaptado Classname Na Função F_BlockHigh [Mehah]
//============================================================
gef_tower.gat,107,35,5	script	Cachorro	81,{
	if(sex ==1) {
		callfunc "F_BlockHigh",Job_Mage_High,""+callfunc("classname",4003,"S","M")+"",Job_High_Wizard,""+callfunc("classname",4010,"S","M")+"","Cachorro";
	}else{
		callfunc "F_BlockHigh",Job_Mage_High,""+callfunc("classname",4003,"S","F")+"",Job_High_Wizard,""+callfunc("classname",4010,"S","F")+"","Cachorro";
	}
	if(BaseJob == Job_Mage) goto L_JobMage;
	callfunc "Wiz1_check";

L_JobMage:
	show "[Cachorro]";
	show "Ah... Eu sei o que você está para dizer.... Quer se tornar um "+callfunc("classname",9,"S","M")+", certo?";
	next;
	show "[Cachorro]";
	show "Fale com a Catherine. Ela vai te ajudar.";
	next;
	show "[Cachorro]";
	show "Se você quiser saber mais sobre o processo da mudança de classe dos "+callfunc("classname",9,"P","M")+", eu posso te ajudar.";
	next;
	menu "..................",M_0, "O cachorro está falando comigo......",M_1;

	M_0:
		show "[Cachorro]";
		show "Woof! O que é isso? Porque você está me olhando desse jeito?? É a primeira vez que você ve um cachorro falando?";
		emotion 1;
		show "[Cachorro]";
		show "Woof! Eu suponho que você não tenha vindo aqui para ver um cachorro falar..... Woof! ..... Éee, é uma situação bem rara....";
		next;
		show "[Cachorro]";
		show "GRRRR!!.... Hey! Pare com essa tolice!! WOOOF!!";
		emotion 6;
		next;

		L_Cont:
		show "[Cachorro]";
		show "Meu nome é Maria Splodofska. Me chame de Maria. Sou assistente dos candidatos a classe dos "+callfunc("classname",9,"P","M")+".";
		next;
		show "[Maria]";
		show "Woof! Veja bem, a razão para me transformar em um cachorro.... Eu estava ajudando meu namorado com a preparação de um experimento.";
		show "Ele está estudando em um outro degrau mágico. Bem, antes de perceber o que tinha acontecido, ele acidentalmente me transformou em um cachorro.....";
		next;
		show "[Maria]";
		show "~Grrrrr~ Em alguns meses, os elementos químicos devem perder seu efeito e eu voltarei ao normal. Teoricamente é o que achamos.";
		next;
		show "[Maria]";
		show "Uh, pois bem..... onde nós estavamos?";
		emotion 20;
		next;
		show "[Maria]";
		show "Como eu disse anteriormente, se você quiser se transformar em um "+callfunc("classname",9,"S","M")+", deve primeiramente falar com a Catherine.";
		show "Ela recentemente se transformou numa "+callfunc("classname",9,"S","F")+", então ela pode ser de grande ajuda.";
		next;
		show "[Maria]";
		show "Eu posso lhe contar mais sobre o processo de mudança de classe, mas eu não quero ocupar mais o seu tempo.";
		show "A menos que, você queira continuar falando sobre a história dos "+callfunc("classname",9,"P","M")+".........";
		next;
		menu "Sim, por favor continue.",sM_0, "Não, tá tudo bem.",sM_1, "Um cachorro falante.....",sM_2;

		sM_0:
			show "[Maria]";
			show "Tá bom então.  Deixe-me explicar o processo para se transformar em um "+callfunc("classname",9,"S","M")+"....";
			next;
			show "[Maria]";
			show "Existem 3 testes na classe dos "+callfunc("classname",9,"P","M")+".  O ^5555FFPrimeiro Teste^000000 será para você coletar os números dos ^FF5555itens mágicos^000000.";
			next;
			show "[Maria]";
			show "Catherine fará o teste. Ela pedirá para você coletar algumas variedades de ^5555FFGemas^000000, ou ^5555FFMinérios Elementais^000000.";
			next;
			show "[Maria]";
			show "O ^5555FFSegundo Teste^000000 será administrado pelo sombrio Raul, ali no canto.";
			show "É um exame de perguntas e respostas sobre os diferentes aspectos da magia.";
			next;
			show "[Maria]";
			show "O teste consistirá em 10 perguntas. Se você falhar em alguma pergunta, falhará no teste.";
			next;
			show "[Maria]";
			show "Raul administrará o ^5555FFTerceiro Teste^000000. É o ultimo teste e você vai precisar derrotar uma série de monstros.";
			show "Haverá três salas, cada uma cheia de monstros de determinados elementos.";
			next;
			show "[Maria]";
			show "É melhor você começar a imaginar quais as melhores magias a serem usadas nesses monstros.";
			next;
			show "[Maria]";
			show "Bem, isso é tudo. Siga em frente e se inscreva.";
			close;
		sM_1:
			show "[Maria]";
			show "Ah, tá. Siga em frente e se inscreva.";
			close;
		sM_2:
			show "[Maria]";
			show "EU NÃO SOU UM CACHORRO!!! IIMMBBEECCIILLLL!!!!";
			emotion 6;
			next;
			show "[Maria]";
			show "Droga! Espero que você FALHE!! Saia daqui, agora!";
			emotion 27;
			next;
			warp "gef_dun00.gat",54,23;
			end;
	M_1:
		show "[Cachorro]";
		show "~WOOF~Woof~woof~ Você NÃO precisa me dizer o óbvio! EU SEI que sou um cachorro!";
		emotion 32;
		next;
		goto L_Cont;

}


//<=========================================== Registrar and Examiner Catherine Medichi ==============================================>\\
gef_tower.gat,111,37,4	script	Catherine Medichi	68,{
	callfunc "F_BlockHigh",26,"High Mage",33,"High Wizard","Catherine Medichi";

	show "[Catherine Medichi]";
	if(BaseJob == Job_Mage) goto L_Mage;
	callfunc "Wiz2_check";



L_Mage:
	if(WIZ_Q == 2) goto L_Test2;
	if(WIZ_Q == 3) goto L_Test3;
	if(WIZ_Q == 4) goto L_Done;
	if(WIZ_Q2 > 0) goto L_Test1;

	show "Meu nome é Catherine Medichi, e eu sou uma "+callfunc("classname",9,"S","F")+". Relaxe, me chame de Catherine.";
	next;
	show "[Catherine Medichi]";
	show "O que o traz até aqui? Você quer ser um "+callfunc("classname",9,"S","M")+", certo?";
	next;
	menu "Claro!",M_0a, "Quais são os requesitos?",M_0b,"Não tenho certeza",M_0End;

	M_0a:
		show "[Catherine Medichi]";
		show "Por favor, preencha este formulário.";
		next;
		show "(formulário preenchido e devolvido)";
		next;
		if(JobLevel < 40) goto sL_JobLvl;
		if(JobLevel == 50) goto sL_Job50;

		show "[Catherine]";
		show "Pois bem ^ff0000"+strcharinfo(0)+"^000000, parece que está tudo em ordem.";
		next;
		show "[Catherine Medichi]";
		show "Agora, deixe-me contar a respeito do teste. Ele é dividido em 3 partes.";
		show "- A primeira parte, envolve em coletar o número dos itens mágicos,";
		show "- A segunda parte, envolve perguntas e respostas,";
		show "- E a terceira parte do teste, minha favorita, é o teste de campo.";
		next;
		show "[Catherine Medichi]";
		show "Pessoas com nível de classe igual a 50, poderão pular a primeira parte do teste. Eles trabalharam muito para isso.";
		next;
		show "[Catherine Medichi]";
		show "Agora, a primeira parte do teste. Requer o recolhimento de um número nos itens mágicos.";
		next;
		set WIZ_Q2, rand(1,2);

		show "[Catherine Medichi]";
		show "Os itens que você precisa trazer são:";
		if(WIZ_Q2 == 2) goto R_2;

		R_1:
			show "- 10 ^ff0000Gemas Vermelhas^000000";
			show "- 10 ^0000ffGemas Azuis^000000";
			show "- e 10 ^ddcc11Gemas Amarelas^000000.";
			goto L_Cont0;

		R_2:
			show "5 ^0000ffCristais Azuis^000000,";
			show "5 ^009900Vidas Verdejantes^000000,";
			show "5 ^ff0000Sangues Escarlates^000000,";
			show "e 5 ^ddcc11Frecores do Vento^000000.";

		L_Cont0:
		next;
		show "[Catherine Medichi]";
		show "Este teste não é TÃO dificil, então vamos andando";
		next;
		show "[Catherine Medichi]";
		show "Boa sorte! Estarei esperando!";
		set WIZ_Q, 1;
		close;

		sL_JobLvl:
			show "[Catherine Medichi]";
			show "Hei, Eu já te disse. Você precisa ter, no mínimo, nível 40 de classe.";
			show "Você não se lembra do que eu te disse?";
			close;

		sL_Job50:
			show "[Catherine Medichi]";
			show "Uau! Você está no nível 50! Deve ter trabalhado bastante! Estou impressionada!";
			next;
			show "[Catherine Medichi]";
			show "Certo, me lembrarei que você tem nível 50 de classe. Você pode pular o primeiro teste, mas ainda restam 2 testes.";
			next;
			show "[Catherine Medichi]";
			show "Agora, converse com aquele homem lá no canto. É difícil, então tenha cuidado.";
			set WIZ_Q, 2;
			close;

	M_0b:
		show "[Catherine Medichi]";
		show "Mesmo que muita gente queira se tornar um "+callfunc("classname",9,"S","M")+", somente aqueles q tem nível 40 de classe se classificam.";
		show "Qualquer um com nível de classe inferior a isso, não tem poder mágico suficiente para se tornar um "+callfunc("classname",9,"S","M")+".";
		next;
		show "[Catherine Medichi]";
		show "Isso é o que importa no teste dos "+callfunc("classname",9,"P","M")+". O teste será feito em 3 etapas.";
		show "Cada etapa tem como foco, diferentes aspectos dos "+callfunc("classname",9,"P","M")+".";
		next;
		show "[Catherine Medichi]";
		show "Uma vez passando nos testes, você será um "+callfunc("classname",9,"S","M")+" que nem eu. Iai!";
		show "Infelizmente, muitas pessoas tem dificuldade nos testes e nunca mais voltam a faze-los.";
		next;
		show "[Catherine Medichi]";
		show "Talvez você tenha uma melhor sorte.";
		close;

	M_0End:
		show "[Catherine Medichi]";
		show "O que? Você veio até aqui para nada!?";
		emotion 1;
		close;


	L_Test1:
		show "Pegou todos os itens? Vejamos......";
		next;
		show "[Catherine Medichi]";
		if(WIZ_Q2 == 1) goto sL_Gems;
		if(WIZ_Q2 == 2) goto sL_Ores;

		sL_Gems:
			if (countitem(716)<10 || countitem(717)<10 || countitem(715)<10) goto ssL_NotDone;
			show "Ótimo! Você trouxe tudo! A Guilda usará sabiamente.";
			next;
			delitem 716, 10;
			delitem 717, 10;
			delitem 715, 10;
			goto ssL_Done;

		sL_Ores:
			if (countitem(991)<4 || countitem(993)<4 || countitem(990)<4 || countitem(992)<4) goto ssL_NotDone;
			show "Ótimo! Você trouxe tudo! A Guilda usará sabiamente.";
			next;
			delitem 991, 5;
			delitem 993, 5;
			delitem 990, 5;
			delitem 992, 5;

			ssL_Done:
				show "[Catherine Medichi]";
				show "Parabéns,você passou no primeiro teste.";
				show "Não relaxe, você ainda tem 2 testes pela frente.";
				emotion 21;
				next;
				show "[Catherine Medichi]";
				show "Agora fale com o homem ali no canto a próxima etapa.";
				show "É um tanto difícil, então tenha cuidado.";
				set WIZ_Q, 2;
				set WIZ_Q2, 0;
				close;

			ssL_NotDone:
				show "Parece que você não trouxe tudo que pedi.";
				next;
				show "[Catherine Medichi]";
				show "É difícil chegar até aqui. Então tenha certeza de estar com todos os itens, tá bem?";
				next;
				show "[Catherine Medichi]";
				show "Você precisa:";
				if(WIZ_Q2 == 1) goto R_1;
				if(WIZ_Q2 == 2) goto R_2;
				close;

	L_Test2:
		if(WIZ_Q2 == 1)goto sL_Failed2;
		show "Hei, eu não disse para você falar com aquele homem? Não? Você não vai se transformar em um "+callfunc("classname",9,"S","M")+" coletando só itens.";
		next;
		show "[Catherine Medichi]";
		show "Existem mais 2 etapas. Estarei esperando, então vá terminar seu teste.";
		close;

		sL_Failed2:
			show "Huh?? Você falhou no teste de perguntas e respostas!?";
			emotion 1;
			next;
			show "[Catherine Medichi]";
			show "Você quer ser um "+callfunc("classname",9,"S","M")+", mas não consegue responder algumas perguntas??";
			emotion 4;
			next;
			show "[Catherine Medichi]";
			show "Bem, Eu posso te dar algumas dicas? Se você quiser, compre um ^009900Suco de Maçã^000000 para mim. Estou com sede.";
			next;
			menu "Qual dica?",M_1a, "Eu passo por mim mesmo!",M_1b;

			M_1a:
				if (countitem(531) < 1) goto sL_NoJuice;
				delitem 531, 1;

				show "[Catherine Medichi]";
				show "Mmmmmmmmmmmmm, Suco de Maçã. ~gulp~gulp~";
				next;
				show "[Catherine Medichi]";
				show "Obrigada, vou te dar algumas dicas.";
				next;
				show "[Catherine Medichi]";
				show "O homem te da 3 tipos de perguntas, sobre:";
				show "- ^ff0000magias^000000,";
				show "- ^ff0000monstros^000000,";
				show "- e ^ff0000caster^000000.";
				next;
				show "[Catherine Medichi]";
				show "Ele quem escolhe as perguntas.";
				next;
				show "[Catherine Medichi]";
				show "Sabe... se ele tiver um bom corte de cabelo e fizer a barba, pode até ter um visual legal...";
				show "Mas óbvio que ele não vai fazer isso....";
				next;
				show "[Catherine Medichi]";
				show "Emfim, a respeito das perguntas sobre as magias, obviamente que você aprendeu algumas magias.";
				next;
				show "[Catherine Medichi]";
				show "Também tem perguntas sobre magias que, talvez, você NÃO saiba, sendo assim vá pegar essas informações com algum "+callfunc("classname",9,"S","M")+".";
				next;
				show "[Catherine Medichi]";
				show "Existem vários lugares quem tem informações sobre magias, como por exemplo www.ragnarok.com.br.";
				show "Você pode procurar procurar em outros lugares também.";
				next;
				show "[Catherine Medichi]";
				show "Quanto as perguntas sobre monstros, você PODERIA lutar contra eles para aprender a respeito deles... OU...";
				next;
				show "[Catherine Medichi]";
				show "... E se me lembro bem, existe uma livraria em Prontera que contêm várias informações sobre monstros.";
				show "Você poderia apenas ir lá, e ler sobre eles.";
				next;
				show "[Catherine Medichie]";
				show "Agora, as perguntas sobre os caster.... Você está por si só..";
				show "Por que? Porque VOCÊ é o caster!";
				next;
				show "[Catherine Medichi]";
				show "Se você não sabe nada sobre você mesmo, como uma desconhecida como vou saber?";
				next;
				show "[Catherine Medichi]";
				show "Bem, boa sorte! Mestre Examinador está esperando!";
				close;

				sL_NoJuice:
					show "[Catherine Medichi]";
					show "Bem, se você quiser algumas dicas, vou precisar de Suco de Maçã! Por outro lado, você está sozinho.";
					close;

			M_1b:
				show "[Catherine Medichi]";
				show "Tá certo! Testes foram feitos para se fazer sozinho! Essa é exatamente a maneira como um "+callfunc("classname",9,"S","M")+" deve pensar!";
				next;
				show "[Catherine Medichi]";
				show "Boa sorte! Mestre Examinador está esperando!";
				close;

	L_Test3:
		if(WIZ_Q2 == 1) goto sL_Failed3;
		show "Hehe, Não pude deixar de escuta-lo durante aquelas perguntas.";
		next;
		show "[Catherine Medichi]";
		show "Bom trabalho, falta só mais um teste! Termine-o e você será um "+callfunc("classname",9,"S","M")+"!";
		next;
		show "[Catherine Medichi]";
		show "Boa sorte! O examinador o espera!";
		close;

		sL_Failed3:
			show "Por que você saiu durante o teste? Nunca pensei que você fosse um fraco...";
			next;
			if(sex==1) goto ssL_M3;
			if(sex==0) goto ssL_F3;

			ssL_M3:
				show "[Catherine Medichi]";
				show "Como pode, um "+callfunc("classname",2,"S","M")+" tão poderoso como você ser derrotado tão facilmente! Volte para lá e tente novamente.";
				close;

			ssL_F3:
				show "[Catherine Medichi]";
				show "Você não pode agir com fraqueza, justo porque você é uma garota! Se você quiser ser uma "+callfunc("classname",9,"S","F")+", Volte e passe no teste!";
				next;
				show "[Catherine Medichi]";
				show "Eu sei que é difícil, mas não era o suficiente para me fazer parar! Você, às vezes, precisa enfrentar seus medos!";
				close;


	L_Done:
		if(SkillPoint > 0) show "Você precisa usar todos os seus pontos de habilidade, antes de se tornar um "+callfunc("classname",9,"S","M")+".";
		if(SkillPoint > 0) close;

		show "Ótimo! Você terminou todos os testes! Parabéns, Parabéns, Parabéns!!";
		emotion 21;
		next;
		show "[Catherine Medichi]";
		show "Ai, estou perdendo tempo, vamos transforma-lo!";
		next;
		callfunc "Job_Change",Job_Wizard;
		callfunc "F_ClearJobVar";		// clears all job variables for the current player

		show "[Catherine Medichi]";
		show "Agora você é um "+callfunc("classname",9,"S","M")+"! Haja com responsabilidade!";
		next;
		show "[Catherine Medichi]";
		show "Tenha certeza quando for gastar suas magias, você sabe exatamente o que pode acontecer!";
		next;
		show "[Catherine Medichi]";
		show "Não faça coisas estúpidas como gastar no meio das cidades!";
		show "Isso causa Lag e as magias foram feitas para serem usadas nos monstros!";
		next;
		show "[Catherine Medichi]";
		show "Torne-se mais poderoso fazendo grupos com outras pessoas! Ah sim, aqui está um presente!";
		next;
		show "[Catherine Medichi]";
		show "Umas Poções Azuis para você ficar feliz...";
		next;
		additem 505, 6;
		show "[Catherine Medichi]";
		show "Use-o sabiamente. É um presente meu, então é melhor você usar do que outras pessoas!";
		next;
		show "[Catherine Medichi]";
		show "Bem, meu companheiro "+callfunc("classname",9,"S","M")+", cuide-se e tenha uma ótima vida!";
		close;
}


//<================================================ Examiner: Raul Expagarus ==================================================>\\
gef_tower.gat,102,24,3	script	Raul Expagarus	735,{
	show "[Raul Expagarus]";
	if(BaseJob == Job_Mage) goto L_Mage;
	if(BaseJob == Job_Wizard) goto L_Wizard;
	if(baseClass == Job_Acolyte) goto L_Holy;
	if(BaseJob == Job_Novice) goto L_Novice;
L_Other:
	show "Ugh, Não me sinto bem.... Sinto como se estivesse preso no gelo........";
	next;
	show "[Raul Expagarus]";
	show "Quem é você?! Saia!";
	emotion 0;
	close;
L_Novice:
	show "O que uma criança como você faz aqui? Sai fora!!";
	emotion 0;
	next;
	warp "geffen.gat",120,110;
	close;
L_Holy:
	show "Deixe o sagrado de lado!";
	show "Mágica é considerado ser um poder e não vem de Deus.......";
	next;
	show "[Raul Expagarus]";
	show "Além de que, eu não estou me sentindo muito bem. Então saia!";
	close;
L_Wizard:
	show "Cof Cof... O que eu posso fazer por você? Hmmm... você é um "+callfunc("classname",2,"S","M")+" também...";
	next;
	show "[Raul Expagarus]";
	if(sex==1) goto L_M;
	if(sex==0) goto L_F;

	L_M:
		show "Se você não for responsável, você pode acidentalmente aprender magias inúteis.";
		show "Entenda isso, você pode querer focalizar na "+callfunc("classname",0,"S","M")+"agem das magias que você estará usando.";
		next;
		show "[Raul Expagarus]";
		show "Em uma luta pelo amor, você pode ser mutilado hehe.";
		close;

	L_F:
		show "Se você não for responsável, você pode acidentalmente aprender magias inúteis.";
		show "Você deve aprender as magias com todo o seu coração ou não aprenderá por completo.";
		next;
		show "[Raul Expagarus]";
		show "Se você não deseja estudar...... Heh, volte então para sua vidinha.";
		close;

L_Mage:
	if(WIZ_Q == 1) goto L_NotRdy;
	if(WIZ_Q == 2) goto L_Test2;
	if(WIZ_Q == 3) goto L_Test3;
	if(WIZ_Q == 4) goto L_Done;

		show "Cof Cof... Eu não tenho tempo para conversar.";
		next;
		show "[Raul Expagarus]";
		show "Desculpe, mas por favor saia.";
		close;

	L_NotRdy:
		show "Heh, um canditato a "+callfunc("classname",9,"S","M")+"?!";
		next;
		show "[Raul Expagarus]";
		show "Você PODE continuar sendo um "+callfunc("classname",2,"S","M")+" e ter uma vida descente.....";
		next;
		show "[Raul Expagarus]";
		show "Então, os "+callfunc("classname",9,"P","M")+" SÃO uma classe muito mais elevada.....";
		close;

	L_Done:
		show "Parabéns por terminar o último teste! Você terminou todos os testes e está pronto para se tornar um "+callfunc("classname",9,"S","M")+"!";
		emotion 21;
		next;
		show "[Raul Expagarus]";
		show "Vá falar com a Catherine para se tornar um "+callfunc("classname",9,"S","M")+". Mas cuidado! A magia é uma força muito poderosa e pode facilmente destruir-lo!!";
		close;

	L_Test2:
		if(WIZ_Q2 == 1) goto sL_ReTest2;

		show "Vejo que passou no primeiro teste. Meu nome é Raul Expagarus, e eu irei examinar o seu teste.";
		next;
		show "[Raul Expagarus]";
		show "Não é tarde demais! Você ainda pode voltar para sua vida pacata na cidade.";
		next;
		show "[Raul Expagarus]";
		show "Talvez você não saiba o quão perigoso seja usar o poder da magia.";
		next;
		menu "Você tem razão. Desisto.",M_0a, "Eu quero fazer o teste!",M_0b;

		sL_ReTest2:
			show "Hmmm, quer tentar denovo?";
			next;
			show "[Raul Expagarus]";
			show "Como você pode odiar tanto uma vida tão calma?";
			next;
			show "[Raul Expagarus]";
			show "Que seja assim! Se você errar uma pergunta, falhou no teste! O que você me diz!?";
			emotion 32;
			next;
			menu "Eu quero morar em paz..........",M_0a, "Eu quero tentar denovo.",M_0b;

		M_0a:
			show "[Raul Expagarus]";
			show "Ótima escolha! Cough cough. Bom, "+callfunc("classname",9,"S","M")+" é uma classe muito difícil, a magia não pode ser usada por qualquer mortal.........";
			next;
			show "[Raul Expagarus]";
			show "Saia fora e não olhe para trás. Use agora a magia que você tem e viva uma boa vida.";
			close;

		M_0b:
			show "[Raul Expagarus]";
			show "Hmmm, veremos o quanto BOM você é.";
			next;
			show "[Raul Expagarus]";
			show "Vamos começar o teste! Se você errar uma pergunta, tá perdido! HA!";
			emotion 29;
			next;
			show "[Raul Expagarus]";
			show "10 perguntas, faça direito! Eu não vou avisar qual pergunta você acertou e qual pergunta você errou.";
			next;
			set @SCORE,0;
			set @temp,rand(0,2);
			if(@temp == 1) goto R_1;
			if(@temp == 2) goto R_2;

			//Spell Quiz
			R_0:
				show "[Raul Expagarus]";
				show "Cof Cof... Aqui estão as perguntas!";
				next;
				show "[Raul Expagarus]";
				show "1. O que NÃO é requisito para Barreira de Fogo?";
				next;
				menu "Lanças de Fogo Nv 4",sM0_0b, "Bolas de Fogo Nv 5",sM0_0b, "Chama Reveladora Nv 1",sM0_0b, "Ataque Espiritual Nv 4",sM0_0a;

				sM0_0a:
					set @SCORE, @SCORE + 10;

				sM0_0b:

				show "[Raul Expagarus]";
				show "2. O que acontece com o elemento de um monstro após ser congelado pela Rajada Congelante?";
				next;
				menu "Muda para Água",sM0_1a, "Muda para Terra",sM0_1b, "Muda para Fogo",sM0_1b, "Muda para Vento",sM0_1b;

				sM0_1a:
					set @SCORE, @SCORE + 10;

				sM0_1b:

				show "[Raul Expagarus]";
				show "3. Quando você maximiza o Ataque Espiritual, quantas vezes o ataque mágico dará de dano?";
				next;
				menu "1.6x",sM0_2b, "1.7x",sM0_2a, "2x",sM0_2b, "20x",sM0_2b;

				sM0_2a:
					set @SCORE, @SCORE + 10;

				sM0_2b:

				show "[Raul Expagarus]";
				show "4. Qual item é requisito para Petrificar?";
				next;
				menu "Sangue Escarlate",sM0_3b, "Gema Azul",sM0_3b, "Gema Amarela",sM0_3b, "Gema Vermelha",sM0_3a;

				sM0_3a:
					set @SCORE, @SCORE + 10;

				sM0_3b:

				show "[Raul Expagarus]";
				show "5. Qual habilidade não é requisito para Escudo Mágico?";
				next;
				menu "Ataque Espiritual Nv 4",sM0_4b, "Espíritos Anciões Nv 5",sM0_4b, "Aumentar Recuperação de SP Nv 6",sM0_4b, "Barreira de Gelo Nv 7",sM0_4a;

				sM0_4a:
					set @SCORE, @SCORE + 10;

				sM0_4b:

				show "[Raul Expagarus]";
				show "6. Quanto SP você irá regenerar, a cada 10 segundos, com nível 7 de Aumentar Recuperação de SP?";
				next;
				menu "14",sM0_5b, "21",sM0_5a, "28",sM0_5b, "35",sM0_5b;

				sM0_5a:
					set @SCORE, @SCORE + 10;

				sM0_5b:

				show "[Raul Expagarus]";
				show "7. Você tem 50% SP, quanto de dano a Proteção Arcana irá reduzir e quanto SP será usado?";
				next;
				menu "Dano 18%, SP 1.5%",sM0_6b, "Dano 18%, SP 2%",sM0_6a, "Dano 24%, SP 1.5%",sM0_6b, "Dano 24%, SP 2%",sM0_6b;

				sM0_6a:
					set @SCORE, @SCORE + 10;

				sM0_6b:

				show "[Raul Expagarus]";
				show "8. Qual o consumo de SP de Escudo Mágico no nível 6, e a quantidade de golpes absorvidos?";
				next;
				menu "SP 40, 6 golpes",sM0_7b, "SP 35, 6 golpes",sM0_7b, "SP 40, 7 golpes",sM0_7a, "SP 35, 7 golpes",sM0_7b;

				sM0_7a:
					set @SCORE, @SCORE + 10;

				sM0_7b:

				show "[Raul Expagarus]";
				show "9. Qual o consumo de SP do Tempestade de Raios no nível 10";
				next;
				menu "84",sM0_8b, "74",sM0_8a, "64",sM0_8b, "54",sM0_8b;

				sM0_8a:
					set @SCORE, @SCORE + 10;

				sM0_8b:

				show "[Raul Expagarus]";
				show "10. Qual é a mallhor magia a ser usada na caverna de Byalan?";
				next;
				menu "Relâmpago",sM0_9a, "Lanças de Fogo",sM0_9b, "Lança de Gelo",sM0_9b, "Chama Reveladora",sM0_9b;

				sM0_9a:
					set @SCORE, @SCORE + 10;

				sM0_9b:
				goto L_Cont0;

			//Monster Quiz
			R_1:
				show "[Raul Expagarus]";
				show "1. Quem solta escudo com slot?";
				next;
				menu "Besouro-Ladrão",sM1_0b, "PecoPeco",sM1_0b, "Pupa",sM1_0a, "Kobold de Maça",sM1_0b;

				sM1_0a:
					set @SCORE, @SCORE + 10;

				sM1_0b:

				show "[Raul Expagarus]";
				show "2. Qual monstro é o mais fácil para "+callfunc("classname",2,"P","M")+"?";
				next;
				menu "Flora",sM1_1a, "Giearth",sM1_1b, "Golem",sM1_1b, "Névoa",sM1_1b;

				sM1_1a:
					set @SCORE, @SCORE + 10;

				sM1_1b:

				show "[Raul Expagarus]";
				show "3. Qual monstro é totalmente imune a Petrificar?";
				next;
				menu "Salgueiro Ancião",sM1_2b, "Druida Malígno",sM1_2a, "Salgueiro",sM1_2b, "Cavalo-Marinho",sM1_2b;

				sM1_2a:
					set @SCORE, @SCORE + 10;

				sM1_2b:
:
				show "[Raul Expagarus]";
				show "4. Quanto de dano você causará com uma magia de vento num monstro água 3?";
				next;
				menu "125%",sM1_3b, "150%",sM1_3b, "175%",sM1_3b, "200%",sM1_3a;

				sM1_3a:
					set @SCORE, @SCORE + 10;

				sM1_3b:

				show "[Raul Expagarus]";
				show "5. Filhote de Lobo do Deserto vs Familiar, quem ganha?";
				next;
				menu "Filhote de Lobo do Deserto",sM1_4a, "Farmiliar",sM1_4b, "Empate",sM1_4b, "Não sei",sM1_4b;

				sM1_4a:
					set @SCORE, @SCORE + 10;

				sM1_4b:

				show "[Raul Expagarus]";
				show "6. Qual monstro não pode ser capturado como animal de estimação?";
				next;
				menu "Poporing",sM1_5b, "Sapo de Rodda",sM1_5a, "Fumacento",sM1_5b, "Esporo Venenoso",sM1_5b;

				sM1_5a:
					set @SCORE, @SCORE + 10;

				sM1_5b:

				show "[Raul Expagarus]";
				show "7. Qual monstro é fraco contra o elemento fogo?.";
				next;
				menu "Goblin de Punhal",sM1_6b, "Goblin de Maça",sM1_6b, "Kobold de Maça",sM1_6b, "Goblin de Martelo",sM1_6a;

				sM1_6a:
					set @SCORE, @SCORE + 10;

				sM1_6b:

				show "[Raul Expagarus]";
				show "8. Qual monstro tem a maior defesa mágica?";
				next;
				menu "Horong",sM1_7b, "ChonChon",sM1_7b, "Andre",sM1_7b, "Caramelo",sM1_7a;

				sM1_7a:
					set @SCORE, @SCORE + 10;

				sM1_7b:

				show "[Raul Expagarus]";
				show "9. Qual monstro não pertence a este grupo.";
				next;
				menu "Poring",sM1_8b, "Mastering",sM1_8b, "Ghostring",sM1_8b, "Esporo",sM1_8a;

				sM1_8a:
					set @SCORE, @SCORE + 10;

				sM1_8b:

				show "[Raul Expagarus]";
				show "10. Qual monstro não é morto-vivo?";
				next;
				menu "Drake",sM1_9b, "Megalodon",sM1_9b, "Deviace",sM1_9a, "Khalitzburg",sM1_9b;

				sM1_9a:
					set @SCORE, @SCORE + 10;

				sM1_9b:
				goto L_Cont0;

			//Wizard Quiz
			R_2:
				show "[Raul Expagarus]";
				show "1. Qual o atributo mais importante para um "+callfunc("classname",9,"S","M")+"?";
				next;
				menu "INT",sM2_0a, "AGI",sM2_0b, "DES",sM2_0b, "VIT",sM2_0b;

				sM2_0a:
					set @SCORE, @SCORE + 10;

				sM2_0b:

				show "[Raul Expagarus]";
				show "2. Qual elemento não tem magias do tipo lanças?";
				next;
				menu "Água",sM2_1b, "Terra",sM2_1a, "Fogo",sM2_1b, "Vento",sM2_1b;

				sM2_1a:
					set @SCORE, @SCORE + 10;

				sM2_1b:

				show "[Raul Expagarus]";
				show "3. Não é caracteristica de um "+callfunc("classname",9,"S","M")+"?";
				next;
				menu "Fracas habilidades físicas",sM2_2b, "Ataques a distância",sM2_2b, "Fazedor de dinheiro",sM2_2a, "Defesa mágica alta",sM2_2b;

				sM2_2a:
					set @SCORE, @SCORE + 10;

				sM2_2b:

				show "[Raul Expagarus]";
				show "4. Qual é a cidade natal dos "+callfunc("classname",9,"P","M")+"?";
				next;
				menu "Prontera",sM2_3b, "Morroc",sM2_3b, "Alberta",sM2_3b, "Geffen",sM2_3a;

				sM2_3a:
					set @SCORE, @SCORE + 10;

				sM2_3b:

				show "[Raul Expagarus]";
				show "5. Qual carta tem nada a ver com o atributo Inteligência?";
				next;
				menu "Carta Andre",sM2_4b, "Carta Andre Soldado",sM2_4a, "Carta Filhote de Lobo do Deserto",sM2_4b, "Carta Salgueiro Ancião",sM2_4b;

				sM2_4a:
					set @SCORE, @SCORE + 10;

				sM2_4b:

				show "[Raul Expagarus]";
				show "6. O que a classe dos "+callfunc("classname",2,"P","M")+" tem de superior sobre as outras classes?";
				next;
				menu "Combate corpo a corpo",sM2_5b, "Esquiva Alta",sM2_5b, "Habilidades para dançar",sM2_5b, "Poderes mágicos",sM2_5a;

				sM2_5a:
					set @SCORE, @SCORE + 10;

				sM2_5b:

				show "[Raul Expagarus]";
				show "7. Qual é o bônus em inteligência no nível 40 de classe?";
				next;
				menu "8",sM2_6b, "7",sM2_6b, "6",sM2_6b, "5",sM2_6a;

				sM2_6a:
					set @SCORE, @SCORE + 10;

				sM2_6b:

				show "[Raul Expagarus]";
				show "8. Qual item não pode ser equipado por "+callfunc("classname",2,"P","M")+"?";
				next;
				menu "Faca",sM2_7b, "Boné",sM2_7a, "Sandália",sM2_7b, "Cajado",sM2_7b;

				sM2_7a:
					set @SCORE, @SCORE + 10;

				sM2_7b:

				show "[Raul Expagarus]";
				show "9. Durante o teste de "+callfunc("classname",2,"S","M")+", Qual pedra NÃO é um catalisador?";
				next;
				menu "Gema Amarela",sM2_8b, "Gema Vermelha",sM2_8b, "Gema Azul",sM2_8b, "Sangue Escarlate",sM2_8a;

				sM2_8a:
					set @SCORE, @SCORE + 10;

				sM2_8b:

				show "[Raul Expagarus]";
				show "10. Qual carta não tem nada a ver com mágica?";
				next;
				menu "Carta Marduk",sM2_9b, "Carta Árvore Maldita",sM2_9a, "Carta Salgueiro",sM2_9b, "Carta Maya",sM2_9b;

				sM2_9a:
					set @SCORE, @SCORE + 10;

				sM2_9b:


			L_Cont0:
			show "[Raul Expagarus]";
			show "Bem, esse é o fim do teste. Enquanto você espera, vá pegar alguma coisa para comer, está bem.";
			next;
			show "[Raul Expagarus]";
			show "Enfim, aqui está o seu resultado: ^ff0000"+@SCORE+"^000000 pontos.";
			next;
			show "[Raul Expagarus]";
			if(@SCORE == 100) goto sL_Pass100;
			if(@SCORE == 90) goto sL_Pass90;

			sL_Failed:
				set WIZ_Q2, 1;
				show "Hmf... Você falhou, vá estudar mais um pouco.";
				next;
				show "[Raul Expagarus]";
				show "Você está no caminho errado. Com essa burrice toda. você não tem chances de sobreviver como "+callfunc("classname",9,"S","M")+".";
				close;

			sL_Pass90:
				set WIZ_Q, 3;
				set WIZ_Q2, 0;
				show "Hmm, você errou uma questão, mas eu vou deixa-lo passar.";
				next;
				show "[Raul Expagarus]";
				show "Não relaxe! Ainda tem o terceiro teste. Enquanto nós preparamos o próximo teste, sente e descanse um pouco.";
				close;

			sL_Pass100:
				set WIZ_Q, 3;
				set WIZ_Q2, 0;
				show "Maravilha! Você passou no segundo teste.";
				next;
				show "[Raul Expagarus]";
				show "Não relaxe! Ainda tem o terceiro teste. Enquanto nós preparamos o próximo teste, sente e descanse um pouco.";
				close;


	L_Test3:
		if(WIZ_Q2 == 1) goto sL_ReTest3;
		show "Descansou? Bom. Agora, vamos começar o último teste.";
		next;
		show "[Raul Expagarus]";
		show "Vou fazer uma breve explicação sobre esse teste, quer escutar?";
		next;
		menu "Não, obrigado.",M_1a, "Por favor, continue",M_1b;

		M_1a:
			show "[Raul Expagarus]";
			show "Seu tolo! tudo bem, se quiser vá morrer! Seu corpo servirá de alimento para os monstros.";
			emotion 23;
			next;
			show "[Raul Expagarus]";
			show "Vou envia-lo agora... Enfim, divirta-se na carnificina!";
			next;
			savepoint "gef_tower.gat",106,29;
			warp "job_wiz.gat",57,154;
			end;

		M_1b:
			show "[Raul Expagarus]";
			show "Boa escolha. Escute cuidadosamente.";
			next;
			show "[Raul Expagarus]";
			show "Esse é um teste para testar as suas habilidades enfrentando os monstros de diferentes tipos elementares.";
			show "Existem 3 diferentes salas, cada sala contêm monstros de diferentes propriedades elementais.";
			next;
			show "[Raul Expagarus]";
			show "Existe a Sala da Água que terá monstros da propriedade Água, seguido pela ";
			show "Sala da Terra que terá monstros da propriedade Terra, e finalmente a Sala de Fogo com monstros da propriedade Fogo.";
			next;
			show "[Raul Expagarus]";
			show "Você terá 3 minutos para cada sala. Você deve matar todos os monstros dentro da sala para poder avançar a próxima sala.";
			next;
			show "[Raul Expagarus]";
			show "Se você usar as habilidades certas você será capaz de terminar este teste facilmente. Isso é tudo.";
			next;
			show "[Raul Expagarus]";
			show "Você parece com medo.... desista agora e volte para a sua cidade.....";
			next;
			menu "Eu não tenho medo! De-me o teste!",sM_a, "... muitooo assustadoorrr... deixe-me voltar pra casa!",sM_b;

			sM_a:
				show "[Raul Expagarus]";
				show "Criança, você tem coragem. Vamos começar o teste agora!";
				next;
				set WIZ_Q2, 1;
				savepoint "gef_tower.gat",106,29;
				warp "job_wiz.gat",57,154;
				end;

			sM_b:
				show "[Raul Expagarus]";
				show "Boa escolha. Tenha uma vida calma e agradável!";
				warp "geffen.gat",120,110;
				end;

		sL_ReTest3:
			show "Você quer fazer o teste DENOVO!? Você ama TANTO ASSIM a magia!?";
			emotion 1;
			next;
			show "[Raul Expagarus]";
			show "Você falhou pela última vez, então tá na hora de mais perguntas! Se não quiser fazer o exame, MUITO MAU!.";
			next;
			show "[Raul Expagarus]";
			show "Vamos começar.";
			next;
			set @SCORE, 0;

			show "[Raul Expagarus]";
			show "1. Qual monstro não existe?";
			next;
			menu "Louva-a-Deus",sM_0b, "Dente de Sabre",sM_0a, "Giearth",sM_0b, "Caramelo",sM_0b;

			sM_0a:
				set @SCORE, @SCORE + 20;

			sM_0b:

			show "[Raul Expagarus]";
			show "2. Qual monstro NÃO é do tipo animal?";
			next;
			menu "Yoyo",sM_1b, "Bigfoot",sM_1b, "Metaller",sM_1b, "Zerom",sM_1a;

			sM_1a:
				set @SCORE, @SCORE + 20;

			sM_1b:

			show "[Raul Expagarus]";
			show "3. Qual monstro não tem sensiblidade para cast?";
			next;
			menu "Marina",sM_2a, "Vitata",sM_2b, "Escorpião",sM_2b, "Giearth",sM_2b;

			sM_2a:
				set @SCORE, @SCORE + 20;

			sM_2b:

			show "[Raul Expagarus]";
			show "4. Qual é a mellhor magia para se usar em umas Esferas Marinhas?";
			next;
			menu "Lanças de Gelo",sM_3b, "Lanças de Fogo",sM_3b, "Relâmpago",sM_3a, "Petrificar",sM_3b;

			sM_3a:
			set @SCORE, @SCORE + 20;

			sM_3b:

			show "[Raul Expagarus]";
			show "5. Qual monstro pode se mover?";
			next;
			menu "Hidra",sM_4b, "Mandragora",sM_4b, "Guardião da Floresta",sM_4b, "Frilldora",sM_4a;

			sM_4a:
				set @SCORE, @SCORE + 20;

			sM_4b:

			show "[Raul Expagarus]";
			show "Seu resultado foi ^ff0000" +@SCORE+ "^000000 pontos.";
			next;
			show "[Raul Expagarus]";
			if (@SCORE >= 80) goto sL_Passed3;

			sL_Failed3:
				show "Você falhou! Vá estudar mais um pouco!";
				next;
				show "[Raul Expagarus]";
				show "Você não é esperto para ser um "+callfunc("classname",9,"S","M")+", porque você acha que falhou!?";
				close;

			sL_Passed3:
				if (@SCORE == 100) show "Se você é tão esperto, porque não passou no teste de primeira?";
				if (@SCORE == 80) show "Hehe, Quase bom. Vou deixa-lo tentar denovo.";
			next;
			menu "Vamos começar!",M_2a, "Explique o teste denovo, por favor.",M_1b;

			M_2a:
				show "[Raul Expagarus]";
				show "Eu não vou ajuda-lo toda vez que você morrer. Não me venha com choro se falhar denovo.... hehe.";
				next;
				percentheal 100,100;
				show "[Raul Expagarus]";
				show "Vou manda-lo para a arena. Diverta-se gritando por sua proteção!";
				next;
				savepoint "gef_tower.gat",106,29;
				warp "job_wiz.gat",55,156;
				end;
}


//<====================================================== Test Arena ========================================================>\\
// Test Guide  ---------------------------------------------------------------------------------
job_wiz.gat,50,165,4	script	Guida do Teste::WzTG	123,{
	show "[Guida do Teste]";
	show "Bem vindo. Vou guia-lo durante o seu teste.";
	next;
	menu "Eu gostaria de fazer o teste.",M_0, "Eu gostaria de ir embora.",M_1;

	M_0:
		show "[Guida do Teste]";
		show "Como foi dito, você terá 3 minutos para terminar com cada sala.";
		show "Eu anunciarei periodicamente quanto tempo lhe resta em cada sala minuto à minuto.";
		next;
		show "[Guida do Teste]";
		show "Por favor, entre na 'Sala de Espera do Teste de "+callfunc("classname",9,"S","M")+"' quando estiver pronto para fazer o teste.";
		show "Quando a sala estiver pronta. você será automaticamente posicionado e o teste terá inicio.";
		next;
		show "[Guida do Teste]";
		show "Boa sorte!";
		close;
	M_1:
		show "[Guida do Teste]";
		show "To vendo. Vou manda-lo devolta.";
		next;
		warp "gef_tower.gat",106, 35;
		end;

OnInit:
	waitingroom "Sala de Espera do Teste de "+callfunc("classname",9,"S","M")+"",8,"WzTG::OnStart",1;
	end;

OnStart:
	set $@WzUsers, getareausers("job_wiz.gat", 98, 154, 129, 185);			// get user count for first lvl
	set $@WzUsers, $@WzUsers + getareausers("job_wiz.gat", 100, 82, 131, 113);		// get user count for second lvl + first lvl
	set $@WzUsers, $@WzUsers + getareausers("job_wiz.gat", 30, 82, 61, 113);		// get user count for third lvl + second lvl + first lvl
	if ($@WzUsers > 0) end;

	if ((getwaitingroomstate(33)) == 0) end;		// finds out if there is anyone in the waiting room
	disablenpc "waterwrp";		//disables the warps
	disablenpc "waterwarpW";
	disablenpc "waterwarpN";
	disablenpc "waterwarpS";
	disablenpc "earthwrp";
	disablenpc "earthwarpW";
	disablenpc "earthwarpN";
	disablenpc "earthwarpS";
	disablenpc "firewrp";
	disablenpc "firewarpN";
	disablenpc "firewarpW";
	disablenpc "firewarpS";
	killmonsterall "job_wiz.gat";		//makes sure there are no left over mobs from other testers
	warpwaitingpc "job_wiz.gat",114,169;
	donpcevent "WaterRm::OnStart";
	end;
}

// Room of Water  -----------------------------------------------------------------------------
job_wiz.gat,1,1,1	script	WaterRm	-1,{

OnStart:
	set $@WzRm, 1;		// used to determine which map area(room) to make time announcements in
	set $@WzMOB, 8;		//used as a counter for the monsters that are summoned
	monster "job_wiz.gat",109,174,"Phen",1158,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",118,174,"Kukre",1070,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",98,170,"Kukre",1070,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",109,165,"Vadon",1066,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",118,165,"Cornutus",1067,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",101,157,"Marina",1141,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",126,157,"Marin",1242,1,"WaterRm::OnMyMobDead1";
	monster "job_wiz.gat",129,170,"Obeaune",1044,1,"WaterRm::OnMyMobDead1";
	initnpctimer "TimerWz";
	end;

OnMyMobDead1:		//when a monster dies this portion of script is run
	set $@WzMOB, $@WzMOB - 1;
	if ($@WzMOB > 0) end;

	stopnpctimer "TimerWz";
	set $@WzMOB, 0;
	addtimer 2000, "WaterRm::OnTimer2000";
	end;

OnTimer2000:
	set $@WzMOB, 5;
	areaannounce "job_wiz.gat", 98, 154, 129, 185, "[Guida do Teste]: Você precisa derrotar os guardiões do portal! Rápido!",8;
	monster "job_wiz.gat",114,169,"Esfera Marinha",1142,1,"WaterRm::OnMyMobDead2";
	monster "job_wiz.gat",112,169,"Hidra",1068,1,"WaterRm::OnMyMobDead2";
	monster "job_wiz.gat",116,169,"Hidra",1068,1,"WaterRm::OnMyMobDead2";
	monster "job_wiz.gat",114,171,"Hidra",1068,1,"WaterRm::OnMyMobDead2";
	monster "job_wiz.gat",114,167,"Hidra",1068,1,"WaterRm::OnMyMobDead2";
	setnpctimer 120000, "TimerWz";
	startnpctimer "TimerWz";
	end;

OnMyMobDead2:
	set $@WzMOB, $@WzMOB - 1;
	if ($@WzMOB > 0) end;

	stopnpctimer "TimerWz";
	set $@WzMOB, 0;
	areaannounce "job_wiz.gat", 98, 154, 129, 185, "[Guida do Teste]: Muito bem. Você completou a Sala de Água. Prepare-se para a Sala da Terra.",8;
	percentheal 100,100;
	enablenpc "waterwrp";
	enablenpc "waterwarpW";
	enablenpc "waterwarpN";
	enablenpc "waterwarpS";
	initnpctimer "waterwrp";		// warps the player after 30 secs even if they don't enter the warps
	end;
}

// Room of Earth -----------------------------------------------------------------------------------------------
job_wiz.gat,1,1,1	script	EarthRm	-1,{

OnStart:
	set $@WzRm, 2;
	set $@WzMOB, 10;
	monster "job_wiz.gat",111,102,"Yoyo",1057,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",120,102, "Deniro",1105,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",111,102, "Caramelo",1103,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",120,102,"Hode",1127,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",111,93,"Selvagem",1166,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",120,93,"Giearth",1121,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",107,98,"Pé-Grande",1160,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",124,98,"Guerreiro Orc",1023,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",104,86,"Vitata",1176,1,"EarthRm::OnMyMobDead1";
	monster "job_wiz.gat",127,86,"Louva-a-Deus",1139,1,"EarthRm::OnMyMobDead1";
	initnpctimer "TimerWz";
	end;

OnMyMobDead1:
	set $@WzMOB, $@WzMOB - 1;
	if ($@WzMOB > 0) end;

	stopnpctimer "TimerWz";
	addtimer 2000, "EarthRm::OnTimer2000";
	set $@WzMOB, 0;
	end;

OnTimer2000:
	set $@WzMOB, 7;
	areaannounce "job_wiz.gat", 100, 82, 131, 113, "[Guida do Teste]: Você precisa derrotar os guardiões do portal! Rápido!",8;
	monster "job_wiz.gat",116,97,"Flora",1118,1,"EarthRm::OnMyMobDead2";
	monster "job_wiz.gat",114,95,"Mandragora",1020,1,"EarthRm::OnMyMobDead2";
	monster "job_wiz.gat",118,95,"Mandragora",1020,1,"EarthRm::OnMyMobDead2";
	monster "job_wiz.gat",114,99,"Mandragora",1020,1,"EarthRm::OnMyMobDead2";
	monster "job_wiz.gat",118,99,"Mandragora",1020,1,"EarthRm::OnMyMobDead2";
	monster "job_wiz.gat",116,94,"Mandragora",1020,1,"EarthRm::OnMyMobDead2";
	monster "job_wiz.gat",116,100,"Mandragora",1020,1,"EarthRm::OnMyMobDead2";
	setnpctimer 120000, "TimerWz";
	startnpctimer "TimerWz";
	end;

OnMyMobDead2:
	set $@WzMOB, $@WzMOB - 1;
	if ($@WzMOB > 0) end;

	stopnpctimer "TimerWz";
	set $@WzMOB, 0;
	areaannounce "job_wiz.gat", 100, 82, 131, 113, "[Guida do Teste]: Muito bem. Você completou a Sala da Terra. Prepare-se para a Sala do Fogo.",8;
	percentheal 100,100;
	enablenpc "earthwrp";
	enablenpc "earthwarpN";
	enablenpc "earthwarpW";
	enablenpc "earthwarpS";
	initnpctimer "earthwrp";
	end;
}

// Room of Fire ------------------------------------------------------------------------------------------------------------
job_wiz.gat,1,1,1	script	FireRm	-1,{

OnStart:
	set $@WzRm, 3;
	set $@WzMOB, 7;
	monster "job_wiz.gat",58,110,"Zerom",1178,1,"FireRm::OnMyMobDead1";
	monster "job_wiz.gat",33,110,"Goblin",1123,1,"FireRm::OnMyMobDead1";
	monster "job_wiz.gat",40,103,"Escorpião",1001,1,"FireRm::OnMyMobDead1";
	monster "job_wiz.gat",51,103,"Frilldora",1119,1,"FireRm::OnMyMobDead1";
	monster "job_wiz.gat",40,92,"PecoPeco",1019,1,"FireRm::OnMyMobDead1";
	monster "job_wiz.gat",51,92,"Salgueiro Ancião",1033,1,"FireRm::OnMyMobDead1";
	monster "job_wiz.gat",37,89,"Metaller",1058,1,"FireRm::OnMyMobDead1";
	initnpctimer "TimerWz";
	end;

OnMyMobDead1:
	set $@WzMOB, $@WzMOB - 1;
	if ($@WzMOB > 0) end;

	stopnpctimer "TimerWz";
	addtimer 2000, "FireRm::OnTimer2000";
	set $@WzMOB, 0;
	end;

OnTimer2000:
	set $@WzMOB, 3;
	areaannounce "job_wiz.gat", 30, 82, 61, 113, "[Guida do Teste]: Você precisa derrotar os guardiões do portal! Rápido!.",8;
	monster "job_wiz.gat",44,99,"Guardião da Floresta",1277,1,"FireRm::OnMyMobDead2";
	monster "job_wiz.gat",43,99,"Horong",1129,1,"FireRm::OnMyMobDead2";
	monster "job_wiz.gat",45,99,"Horong",1129,1,"FireRm::OnMyMobDead2";
	setnpctimer 120000, "TimerWz";
	startnpctimer "TimerWz";
	end;

OnMyMobDead2:
	set $@WzMOB, $@WzMOB - 1;
	if ($@WzMOB > 0) end;

	stopnpctimer "TimerWz";
	set $@WzMOB, 0;
	set WIZ_Q, 4;
	set WIZ_Q2, 0;
	areaannounce "job_wiz.gat", 30, 82, 61, 113, "[Guida do Teste]: Muito bem. Você completou a Sala do Fogo e o teste inteiro.",0;
	enablenpc "firewrp";
	enablenpc "firewarpN";
	enablenpc "firewarpW";
	enablenpc "firewarpS";
	initnpctimer "firewrp";
	end;
}


// Timer -----------------------------------------------------------------------------------------------
job_wiz.gat,1,1,1	script	TimerWz	-1,{

OnTimer1000:
	if($@WzRm==1) areaannounce "job_wiz.gat", 98, 154, 129, 185, "[Guida do Teste]: Sala da Água, teste terá início.",8;
	if($@WzRm==2) areaannounce "job_wiz.gat", 100, 82, 131, 113, "[Guida do Teste]: Sala da Terra, teste terá início.",8;
	if($@WzRm==3) areaannounce "job_wiz.gat", 30, 82, 61, 113, "[Guida do Teste]: Sala do Fogo, teste terá início.",8;
	end;
OnTimer4000:
	set $@WzTime$, "Você tem 3 minutos para derrotar todos os monstros nessa sala, começando agora!";
	donpcevent "WzTG::OnStart";		//checks to see if anyone is still in the room, in case of logout or KO
	callsub AnnounceWz;
OnTimer34000:
	donpcevent "WzTG::OnStart";
	end;
OnTimer64000:
	set $@WzTime$, "Você tem mais 2 minutos!";
	donpcevent "WzTG::OnStart";
	callsub AnnounceWz;
OnTimer94000:
	donpcevent "WzTG::OnStart";
	end;
OnTimer124000:
	set $@WzTime$, "Você tem mais 1 minuto!";
	donpcevent "WzTG::OnStart";
	callsub AnnounceWz;
OnTimer154000:
	set $@WzTime$, "Você tem mais 30 segundos!";
	donpcevent "WzTG::OnStart";
	callsub AnnounceWz;
OnTimer174000:
	set $@WzTime$, "Você tem mais 10 segundos!";
	callsub AnnounceWz;
OnTimer184000:
	set $@WzTime$, "Tempo acabou. Você falhou no teste.";
	callsub AnnounceWz;
OnTimer188000:
	stopnpctimer;
	if($@WzRm==1) areawarp "job_wiz.gat", 98, 154, 129, 185, "gef_tower.gat",106, 35;
	if($@WzRm==2) areawarp "job_wiz.gat", 100, 82, 131, 113, "gef_tower.gat",106, 35;
	if($@WzRm==3) areawarp "job_wiz.gat", 30, 82, 61, 113, "gef_tower.gat",106, 35;
	killmonsterall "job_wiz.gat";
	donpcevent "WzTG::OnStart";
	end;

AnnounceWz:
	if($@WzRm==1) areaannounce "job_wiz.gat", 98, 154, 129, 185, "[Guia do Teste]: "+$@WzTime$+".",8;
	if($@WzRm==2) areaannounce "job_wiz.gat", 100, 82, 131, 113, "[Guia do Teste]: "+$@WzTime$+".",8;
	if($@WzRm==3) areaannounce "job_wiz.gat", 30, 82, 61, 113, "[Guia do Teste]: "+$@WzTime$+".",8;
	end;
}


// Warps ------------------------------------------------------------------------------------------

// Water
job_wiz.gat,129,169,1	script	waterwarpE::waterwrp	45,1,1,{

OnTimer30000:
	stopnpctimer "waterwrp";
	areawarp "job_wiz.gat", 98, 154, 129, 185, "job_wiz.gat",116,98;
	donpcevent "EarthRm::OnStart";
	end;
}
job_wiz.gat,98,169,1	duplicate(waterwrp)	waterwarpW	45,1,1
job_wiz.gat,114,185,1	duplicate(waterwrp)	waterwarpN	45,1,1
job_wiz.gat,114,154,1	duplicate(waterwrp)	waterwarpS	45,1,1


// Earth
job_wiz.gat,131,98,1	script	earthwarpE::earthwrp	45,1,1,{

OnTimer30000:
	stopnpctimer "earthwrp";
	areawarp "job_wiz.gat", 100, 82, 131, 113, "job_wiz.gat",47,98;
	donpcevent "FireRm::OnStart";
	end;
}
job_wiz.gat,100,98,1	duplicate(earthwrp)	earthwarpW	45,1,1
job_wiz.gat,116,82,1	duplicate(earthwrp)	earthwarpN	45,1,1
job_wiz.gat,116,113,1	duplicate(earthwrp)	earthwarpS	45,1,1


// Fire
job_wiz.gat,61,97,1	script	firewarpE::firewrp	45,1,1,{

OnTimer30000:
	stopnpctimer "firewrp";
	areawarp "job_wiz.gat", 30, 82, 61, 113, "gef_tower.gat",106,35;
	donpcevent "WzTG::OnStart";
	end;
}
job_wiz.gat,30,97,1	duplicate(firewrp)	firewarpW	45,1,1
job_wiz.gat,46,113,1	duplicate(firewrp)	firewarpN	45,1,1
job_wiz.gat,46,82,1	duplicate(firewrp)	firewarpS	45,1,1


//==============================================================================
// mapflag
//==============================================================================
job_wiz.gat	mapflag	nomemo
job_wiz.gat	mapflag	noteleport
job_wiz.gat	mapflag	nosave	SavePoint
job_wiz.gat	mapflag	nopenalty
job_wiz.gat	mapflag	nobranch
job_wiz.gat	mapflag	noexp
job_wiz.gat	mapflag	noloot